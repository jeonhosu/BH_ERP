CREATE OR REPLACE PACKAGE HRP_PERSON_PAYMENT_RULE_G
AS

-- SELECT_PERSON_PAYMENT_RULE
  PROCEDURE SELECT_PERSON_PAYMENT_RULE
            ( P_CURSOR            OUT TYPES.TCURSOR
            , W_CORP_ID           IN HRP_PERSON_PAYMENT_RULE.CORP_ID%TYPE
            , W_STD_YYYYMM        IN HRP_PERSON_PAYMENT_RULE.EFFECTIVE_YYYYMM_FR%TYPE
            , W_DEPT_ID           IN HRM_DEPT_MASTER.DEPT_ID%TYPE
            , W_PERSON_ID         IN HRP_PERSON_PAYMENT_RULE.PERSON_ID%TYPE
            , W_SOB_ID            IN HRP_PERSON_PAYMENT_RULE.SOB_ID%TYPE
            , W_ORG_ID            IN HRP_PERSON_PAYMENT_RULE.ORG_ID%TYPE
            );

---------------------------------------------------------------------------------------------------
-- PERSON_PAYMENT_RULE_INSERT
  PROCEDURE PERSON_PAYMENT_RULE_INSERT
            ( P_PERSON_RULE_ID      OUT HRP_PERSON_PAYMENT_RULE.PERSON_RULE_ID%TYPE
            , P_CORP_ID             IN HRP_PERSON_PAYMENT_RULE.CORP_ID%TYPE
            , P_PERSON_ID           IN HRP_PERSON_PAYMENT_RULE.PERSON_ID%TYPE
            , P_PAYMENT_RATE        IN HRP_PERSON_PAYMENT_RULE.PAYMENT_RATE%TYPE
            , P_DESCRIPTION         IN HRP_PERSON_PAYMENT_RULE.DESCRIPTION%TYPE
            , P_ENABLED_FLAG        IN HRP_PERSON_PAYMENT_RULE.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_YYYYMM_FR IN HRP_PERSON_PAYMENT_RULE.EFFECTIVE_YYYYMM_FR%TYPE
            , P_EFFECTIVE_YYYYMM_TO IN HRP_PERSON_PAYMENT_RULE.EFFECTIVE_YYYYMM_TO%TYPE
            , P_SOB_ID              IN HRP_PERSON_PAYMENT_RULE.SOB_ID%TYPE
            , P_ORG_ID              IN HRP_PERSON_PAYMENT_RULE.ORG_ID%TYPE
            , P_USER_ID             IN HRP_PERSON_PAYMENT_RULE.CREATED_BY%TYPE
            );

-- PERSON_PAYMENT_RULE_UPDATE.
  PROCEDURE PERSON_PAYMENT_RULE_UPDATE
            ( W_PERSON_RULE_ID      IN HRP_PERSON_PAYMENT_RULE.PERSON_RULE_ID%TYPE
            , P_PAYMENT_RATE        IN HRP_PERSON_PAYMENT_RULE.PAYMENT_RATE%TYPE
            , P_DESCRIPTION         IN HRP_PERSON_PAYMENT_RULE.DESCRIPTION%TYPE
            , P_ENABLED_FLAG        IN HRP_PERSON_PAYMENT_RULE.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_YYYYMM_FR IN HRP_PERSON_PAYMENT_RULE.EFFECTIVE_YYYYMM_FR%TYPE
            , P_EFFECTIVE_YYYYMM_TO IN HRP_PERSON_PAYMENT_RULE.EFFECTIVE_YYYYMM_TO%TYPE
            , P_SOB_ID              IN HRP_PERSON_PAYMENT_RULE.SOB_ID%TYPE
            , P_ORG_ID              IN HRP_PERSON_PAYMENT_RULE.ORG_ID%TYPE
            , P_USER_ID             IN HRP_PERSON_PAYMENT_RULE.CREATED_BY%TYPE
            );

END HRP_PERSON_PAYMENT_RULE_G;
/
CREATE OR REPLACE PACKAGE BODY HRP_PERSON_PAYMENT_RULE_G
AS
/******************************************************************************/
/* PROJECT      : FPCB ERP
/* MODULE       : EAPP
/* PROGRAM NAME : HRP_PERSON_PAYMENT_RULE_G
/* DESCRIPTION  : 급상여 개인별 지급율 관리.
/* REFERENCE BY :
/* PROGRAM HISTORY : 신규 생성
/*------------------------------------------------------------------------------
/*   DATE       IN CHARGE          DESCRIPTION
/*------------------------------------------------------------------------------
/* 20-JUN-2010  JEON HO SU          INITIALIZE
/******************************************************************************/
-- SELECT_PERSON_PAYMENT_RULE
  PROCEDURE SELECT_PERSON_PAYMENT_RULE
            ( P_CURSOR            OUT TYPES.TCURSOR
            , W_CORP_ID           IN HRP_PERSON_PAYMENT_RULE.CORP_ID%TYPE
            , W_STD_YYYYMM        IN HRP_PERSON_PAYMENT_RULE.EFFECTIVE_YYYYMM_FR%TYPE
            , W_DEPT_ID           IN HRM_DEPT_MASTER.DEPT_ID%TYPE
            , W_PERSON_ID         IN HRP_PERSON_PAYMENT_RULE.PERSON_ID%TYPE
            , W_SOB_ID            IN HRP_PERSON_PAYMENT_RULE.SOB_ID%TYPE
            , W_ORG_ID            IN HRP_PERSON_PAYMENT_RULE.ORG_ID%TYPE
            )
  AS
    V_END_DATE                    DATE;
    
  BEGIN
    V_END_DATE := LAST_DAY(TO_DATE(W_STD_YYYYMM, 'YYYY-MM'));
    
    OPEN P_CURSOR FOR
      SELECT T1.PERSON_ID    
           , PM.NAME
           , PM.PERSON_NUM
           , HRM_DEPT_MASTER_G.DEPT_NAME_F(HL.DEPT_ID) AS DEPT_NAME
           , HRM_COMMON_G.ID_NAME_F(HL.POST_ID) AS POST_NAME
           , PM.ORI_JOIN_DATE
           , PM.JOIN_DATE
           , PM.RETIRE_DATE
           , T1.PERSON_RULE_ID
           , T1.PAYMENT_RATE
           , T1.DESCRIPTION
           , T1.ENABLED_FLAG
           , T1.EFFECTIVE_YYYYMM_FR
           , T1.EFFECTIVE_YYYYMM_TO
           , T1.CORP_ID
        FROM HRM_HISTORY_LINE HL  
          , HRM_PERSON_MASTER PM
          , ( SELECT PPR.PERSON_RULE_ID
                   , PPR.CORP_ID
                   , PPR.PERSON_ID
                   , PPR.PAYMENT_RATE
                   , PPR.DESCRIPTION
                   , PPR.ENABLED_FLAG
                   , PPR.EFFECTIVE_YYYYMM_FR
                   , PPR.EFFECTIVE_YYYYMM_TO
                FROM HRP_PERSON_PAYMENT_RULE PPR
               WHERE PPR.CORP_ID                = W_CORP_ID
                 AND PPR.PERSON_ID              = NVL(W_PERSON_ID, PPR.PERSON_ID)
            ) T1
      WHERE HL.PERSON_ID          = PM.PERSON_ID
        AND PM.PERSON_ID          = T1.PERSON_ID
        AND HL.HISTORY_LINE_ID    IN ( SELECT MAX(S_HL.HISTORY_LINE_ID) AS HISTORY_LINE_ID
                                        FROM HRM_HISTORY_LINE S_HL
                                       WHERE S_HL.CHARGE_DATE            <= V_END_DATE
                                         AND S_HL.PERSON_ID              = HL.PERSON_ID
                                       GROUP BY S_HL.PERSON_ID
                                     )
			  AND PM.CORP_ID               = W_CORP_ID
        AND HL.DEPT_ID               = NVL(W_DEPT_ID, HL.DEPT_ID)
        AND T1.PERSON_ID             = NVL(W_PERSON_ID, PM.PERSON_ID)
        AND PM.SOB_ID                = W_SOB_ID
        AND PM.ORG_ID                = W_ORG_ID
      ORDER BY HL.DEPT_ID, HL.POST_ID, HL.PERSON_ID
			;	     

  END SELECT_PERSON_PAYMENT_RULE;

---------------------------------------------------------------------------------------------------
-- PERSON_PAYMENT_RULE_INSERT
  PROCEDURE PERSON_PAYMENT_RULE_INSERT
            ( P_PERSON_RULE_ID      OUT HRP_PERSON_PAYMENT_RULE.PERSON_RULE_ID%TYPE
            , P_CORP_ID             IN HRP_PERSON_PAYMENT_RULE.CORP_ID%TYPE
            , P_PERSON_ID           IN HRP_PERSON_PAYMENT_RULE.PERSON_ID%TYPE
            , P_PAYMENT_RATE        IN HRP_PERSON_PAYMENT_RULE.PAYMENT_RATE%TYPE
            , P_DESCRIPTION         IN HRP_PERSON_PAYMENT_RULE.DESCRIPTION%TYPE
            , P_ENABLED_FLAG        IN HRP_PERSON_PAYMENT_RULE.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_YYYYMM_FR IN HRP_PERSON_PAYMENT_RULE.EFFECTIVE_YYYYMM_FR%TYPE
            , P_EFFECTIVE_YYYYMM_TO IN HRP_PERSON_PAYMENT_RULE.EFFECTIVE_YYYYMM_TO%TYPE
            , P_SOB_ID              IN HRP_PERSON_PAYMENT_RULE.SOB_ID%TYPE
            , P_ORG_ID              IN HRP_PERSON_PAYMENT_RULE.ORG_ID%TYPE
            , P_USER_ID             IN HRP_PERSON_PAYMENT_RULE.CREATED_BY%TYPE
            )
  AS
    V_SYSDATE                     DATE := GET_LOCAL_DATE(P_SOB_ID);

  BEGIN
    BEGIN
      SELECT HRP_PERSON_PAYMENT_RULE_S1.NEXTVAL
         INTO P_PERSON_RULE_ID
         FROM DUAL;
    EXCEPTION WHEN OTHERS THEN
      RAISE ERRNUMS.Invalid_Sequence_ID;
    END;

    INSERT INTO HRP_PERSON_PAYMENT_RULE
    ( PERSON_RULE_ID
    , CORP_ID 
    , PERSON_ID 
    , PAYMENT_RATE 
    , DESCRIPTION 
    , ENABLED_FLAG 
    , EFFECTIVE_YYYYMM_FR 
    , EFFECTIVE_YYYYMM_TO 
    , SOB_ID 
    , ORG_ID 
    , CREATION_DATE 
    , CREATED_BY 
    , LAST_UPDATE_DATE 
    , LAST_UPDATED_BY 
    ) VALUES
    ( P_PERSON_RULE_ID
    , P_CORP_ID
    , P_PERSON_ID
    , P_PAYMENT_RATE
    , P_DESCRIPTION
    , P_ENABLED_FLAG
    , P_EFFECTIVE_YYYYMM_FR
    , P_EFFECTIVE_YYYYMM_TO
    , P_SOB_ID
    , P_ORG_ID
    , V_SYSDATE
    , P_USER_ID
    , V_SYSDATE
    , P_USER_ID 
    );

  EXCEPTION
    WHEN ERRNUMS.Invalid_Sequence_ID THEN
      RAISE_APPLICATION_ERROR(ERRNUMS.Invalid_Sequence_Code, ERRNUMS.Invalid_Sequence_Desc);
  END PERSON_PAYMENT_RULE_INSERT;

-- PERSON_PAYMENT_RULE_UPDATE.
  PROCEDURE PERSON_PAYMENT_RULE_UPDATE
            ( W_PERSON_RULE_ID      IN HRP_PERSON_PAYMENT_RULE.PERSON_RULE_ID%TYPE
            , P_PAYMENT_RATE        IN HRP_PERSON_PAYMENT_RULE.PAYMENT_RATE%TYPE
            , P_DESCRIPTION         IN HRP_PERSON_PAYMENT_RULE.DESCRIPTION%TYPE
            , P_ENABLED_FLAG        IN HRP_PERSON_PAYMENT_RULE.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_YYYYMM_FR IN HRP_PERSON_PAYMENT_RULE.EFFECTIVE_YYYYMM_FR%TYPE
            , P_EFFECTIVE_YYYYMM_TO IN HRP_PERSON_PAYMENT_RULE.EFFECTIVE_YYYYMM_TO%TYPE
            , P_SOB_ID              IN HRP_PERSON_PAYMENT_RULE.SOB_ID%TYPE
            , P_ORG_ID              IN HRP_PERSON_PAYMENT_RULE.ORG_ID%TYPE
            , P_USER_ID             IN HRP_PERSON_PAYMENT_RULE.CREATED_BY%TYPE
            )
  AS
   V_SYSDATE DATE := GET_LOCAL_DATE(P_SOB_ID);

  BEGIN
    UPDATE HRP_PERSON_PAYMENT_RULE
      SET PAYMENT_RATE        = P_PAYMENT_RATE
        , DESCRIPTION         = P_DESCRIPTION
        , ENABLED_FLAG        = P_ENABLED_FLAG
        , EFFECTIVE_YYYYMM_FR = P_EFFECTIVE_YYYYMM_FR
        , EFFECTIVE_YYYYMM_TO = P_EFFECTIVE_YYYYMM_TO
        , SOB_ID              = P_SOB_ID
        , ORG_ID              = P_ORG_ID
        , LAST_UPDATE_DATE    = V_SYSDATE
        , LAST_UPDATED_BY     = P_USER_ID
    WHERE PERSON_RULE_ID      = W_PERSON_RULE_ID;

  END PERSON_PAYMENT_RULE_UPDATE;

END HRP_PERSON_PAYMENT_RULE_G;
/
