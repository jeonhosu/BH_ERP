CREATE OR REPLACE PACKAGE HRM_AUTO_DOCUMENT_G
AS

-- 자동분개유형 헤더 조회.
  PROCEDURE SELECT_AUTO_DOCUMENT_H
            ( P_CURSOR               OUT TYPES.TCURSOR
            , W_JOB_CODE             IN HRM_AUTO_DOCUMENT_HEADER.JOB_CODE%TYPE
            , W_SOB_ID               IN HRM_AUTO_DOCUMENT_HEADER.SOB_ID%TYPE
            , W_ORG_ID               IN HRM_AUTO_DOCUMENT_HEADER.ORG_ID%TYPE
            );

-- 자동분개유형 헤더 삽입.
  PROCEDURE INSERT_AUTO_DOCUMENT_H
            ( P_DOCUMENT_HEADER_ID OUT HRM_AUTO_DOCUMENT_HEADER.DOCUMENT_HEADER_ID%TYPE
            , P_SOB_ID             IN HRM_AUTO_DOCUMENT_HEADER.SOB_ID%TYPE
            , P_ORG_ID             IN HRM_AUTO_DOCUMENT_HEADER.ORG_ID%TYPE
            , P_SLIP_TYPE          IN HRM_AUTO_DOCUMENT_HEADER.SLIP_TYPE%TYPE
            , P_JOB_CODE           IN HRM_AUTO_DOCUMENT_HEADER.JOB_CODE%TYPE
            , P_JOB_DESC           IN HRM_AUTO_DOCUMENT_HEADER.JOB_DESC%TYPE
            , P_REMARK             IN HRM_AUTO_DOCUMENT_HEADER.REMARK%TYPE
            , P_ENABLED_FLAG       IN HRM_AUTO_DOCUMENT_HEADER.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR  IN HRM_AUTO_DOCUMENT_HEADER.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO  IN HRM_AUTO_DOCUMENT_HEADER.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID            IN HRM_AUTO_DOCUMENT_HEADER.CREATED_BY%TYPE
            );

-- 자동분개유형 헤더 수정.
  PROCEDURE UPDATE_AUTO_DOCUMENT_H
            ( W_DOCUMENT_HEADER_ID IN HRM_AUTO_DOCUMENT_HEADER.DOCUMENT_HEADER_ID%TYPE
            , P_SOB_ID             IN HRM_AUTO_DOCUMENT_HEADER.SOB_ID%TYPE
            , P_ORG_ID             IN HRM_AUTO_DOCUMENT_HEADER.ORG_ID%TYPE
            , P_JOB_DESC           IN HRM_AUTO_DOCUMENT_HEADER.JOB_DESC%TYPE
            , P_REMARK             IN HRM_AUTO_DOCUMENT_HEADER.REMARK%TYPE
            , P_ENABLED_FLAG       IN HRM_AUTO_DOCUMENT_HEADER.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR  IN HRM_AUTO_DOCUMENT_HEADER.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO  IN HRM_AUTO_DOCUMENT_HEADER.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID            IN HRM_AUTO_DOCUMENT_HEADER.CREATED_BY%TYPE
            );

---------------------------------------------------------------------------------------------------
-- 자동분개유형 라인 조회.
  PROCEDURE SELECT_AUTO_DOCUMENT_L
            ( P_CURSOR1              OUT TYPES.TCURSOR1
            , W_DOCUMENT_HEADER_ID   IN HRM_AUTO_DOCUMENT_LINE.DOCUMENT_HEADER_ID%TYPE
            , W_SOB_ID               IN HRM_AUTO_DOCUMENT_LINE.SOB_ID%TYPE
            , W_ORG_ID               IN HRM_AUTO_DOCUMENT_LINE.ORG_ID%TYPE
            );

-- 자동분개유형 라인 삽입.
  PROCEDURE INSERT_AUTO_DOCUMENT_L
            ( P_DOCUMENT_LINE_ID   OUT HRM_AUTO_DOCUMENT_LINE.DOCUMENT_LINE_ID%TYPE
            , P_SOB_ID             IN HRM_AUTO_DOCUMENT_LINE.SOB_ID%TYPE
            , P_ORG_ID             IN HRM_AUTO_DOCUMENT_LINE.ORG_ID%TYPE
            , P_DOCUMENT_HEADER_ID IN HRM_AUTO_DOCUMENT_LINE.DOCUMENT_HEADER_ID%TYPE
            , P_ACCOUNT_CONTROL_ID IN HRM_AUTO_DOCUMENT_LINE.ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE       IN VARCHAR2
            , P_ACCOUNT_DR_CR      IN HRM_AUTO_DOCUMENT_LINE.ACCOUNT_DR_CR%TYPE
            , P_REMARK             IN HRM_AUTO_DOCUMENT_LINE.REMARK%TYPE
            , P_VAT_YN             IN HRM_AUTO_DOCUMENT_LINE.VAT_YN%TYPE
            , P_CONTRA_ACCOUNT_YN  IN HRM_AUTO_DOCUMENT_LINE.CONTRA_ACCOUNT_YN%TYPE
            , P_DIR_INDIR_TYPE     IN HRM_AUTO_DOCUMENT_LINE.DIR_INDIR_TYPE%TYPE
            , P_VENDOR_CODE        IN HRM_AUTO_DOCUMENT_LINE.VENDOR_CODE%TYPE
            , P_ENABLED_FLAG       IN HRM_AUTO_DOCUMENT_LINE.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR  IN HRM_AUTO_DOCUMENT_LINE.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO  IN HRM_AUTO_DOCUMENT_LINE.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID            IN HRM_AUTO_DOCUMENT_LINE.CREATED_BY%TYPE
            );

-- 자동분개유형 라인 수정.
  PROCEDURE UPDATE_AUTO_DOCUMENT_L
            ( W_DOCUMENT_LINE_ID   IN HRM_AUTO_DOCUMENT_LINE.DOCUMENT_LINE_ID%TYPE
            , P_SOB_ID             IN HRM_AUTO_DOCUMENT_LINE.SOB_ID%TYPE
            , P_ORG_ID             IN HRM_AUTO_DOCUMENT_LINE.ORG_ID%TYPE
            , P_ACCOUNT_CONTROL_ID IN HRM_AUTO_DOCUMENT_LINE.ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE       IN VARCHAR2
            , P_ACCOUNT_DR_CR      IN HRM_AUTO_DOCUMENT_LINE.ACCOUNT_DR_CR%TYPE
            , P_REMARK             IN HRM_AUTO_DOCUMENT_LINE.REMARK%TYPE
            , P_VAT_YN             IN HRM_AUTO_DOCUMENT_LINE.VAT_YN%TYPE
            , P_CONTRA_ACCOUNT_YN  IN HRM_AUTO_DOCUMENT_LINE.CONTRA_ACCOUNT_YN%TYPE
            , P_DIR_INDIR_TYPE     IN HRM_AUTO_DOCUMENT_LINE.DIR_INDIR_TYPE%TYPE
            , P_VENDOR_CODE        IN HRM_AUTO_DOCUMENT_LINE.VENDOR_CODE%TYPE
            , P_ENABLED_FLAG       IN HRM_AUTO_DOCUMENT_LINE.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR  IN HRM_AUTO_DOCUMENT_LINE.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO  IN HRM_AUTO_DOCUMENT_LINE.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID            IN HRM_AUTO_DOCUMENT_LINE.CREATED_BY%TYPE 
            );

-- 자동분개유형 라인 삭제.
  PROCEDURE DELETE_AUTO_DOCUMENT_L
            ( W_DOCUMENT_LINE_ID   IN HRM_AUTO_DOCUMENT_LINE.DOCUMENT_LINE_ID%TYPE
            , P_SOB_ID             IN HRM_AUTO_DOCUMENT_LINE.SOB_ID%TYPE
            , P_ORG_ID             IN HRM_AUTO_DOCUMENT_LINE.ORG_ID%TYPE
            );
            
---------------------------------------------------------------------------------------------------
-- 자동분개유형 급상여 지급/공제 항목 조회.
  PROCEDURE SELECT_AUTO_DOCUMENT_ALLOWANCE
            ( P_CURSOR1              OUT TYPES.TCURSOR1
            , W_DOCUMENT_LINE_ID     IN HRM_AUTO_DOCUMENT_ALLOWANCE.DOCUMENT_LINE_ID%TYPE
            , W_SOB_ID               IN HRM_AUTO_DOCUMENT_LINE.SOB_ID%TYPE
            , W_ORG_ID               IN HRM_AUTO_DOCUMENT_LINE.ORG_ID%TYPE
            );

-- 자동분개유형 급상여 지급/공제 항목 삽입.
  PROCEDURE INSERT_DOCUMENT_ALLOWANCE
            ( P_DOCUMENT_ALLOWANCE_ID OUT HRM_AUTO_DOCUMENT_ALLOWANCE.DOCUMENT_ALLOWANCE_ID%TYPE
            , P_SOB_ID                IN NUMBER
            , P_DOCUMENT_LINE_ID      IN HRM_AUTO_DOCUMENT_ALLOWANCE.DOCUMENT_LINE_ID%TYPE
            , P_ALLOWANCE_TYPE        OUT HRM_AUTO_DOCUMENT_ALLOWANCE.ALLOWANCE_TYPE%TYPE
            , P_ALLOWANCE_ID          IN HRM_AUTO_DOCUMENT_ALLOWANCE.ALLOWANCE_ID%TYPE
            , P_ALLOWANCE_CODE        IN VARCHAR2            
            , P_USER_ID               IN HRM_AUTO_DOCUMENT_ALLOWANCE.CREATED_BY%TYPE
            );

-- 자동분개유형 급상여 지급/공제 항목 수정.
  PROCEDURE UPDATE_DOCUMENT_ALLOWANCE
            ( W_DOCUMENT_ALLOWANCE_ID  IN HRM_AUTO_DOCUMENT_ALLOWANCE.DOCUMENT_ALLOWANCE_ID%TYPE
             , P_SOB_ID                IN NUMBER
             , P_DOCUMENT_LINE_ID      IN HRM_AUTO_DOCUMENT_ALLOWANCE.DOCUMENT_LINE_ID%TYPE
             , P_ALLOWANCE_TYPE        OUT HRM_AUTO_DOCUMENT_ALLOWANCE.ALLOWANCE_TYPE%TYPE
             , P_ALLOWANCE_ID          IN HRM_AUTO_DOCUMENT_ALLOWANCE.ALLOWANCE_ID%TYPE
             , P_ALLOWANCE_CODE        IN VARCHAR2
             , P_USER_ID               IN HRM_AUTO_DOCUMENT_ALLOWANCE.CREATED_BY%TYPE
            );

-- 자동분개유형 급상여 지급/공제 항목 삭제.
  PROCEDURE DELETE_DOCUMENT_ALLOWANCE
            ( W_DOCUMENT_ALLOWANCE_ID  IN HRM_AUTO_DOCUMENT_ALLOWANCE.DOCUMENT_ALLOWANCE_ID%TYPE
            );
            
---------------------------------------------------------------------------------------------------
-- 자동분개유형 업무구분 조회.
  PROCEDURE LU_JOB_CODE
            ( P_CURSOR3              OUT TYPES.TCURSOR3
            , W_SOB_ID               IN HRM_AUTO_DOCUMENT_HEADER.SOB_ID%TYPE
            , W_SLIP_TYPE            IN HRM_AUTO_DOCUMENT_HEADER.SLIP_TYPE%TYPE
            , W_ENABLED_YN           IN HRM_AUTO_DOCUMENT_HEADER.ENABLED_FLAG%TYPE
            );


END HRM_AUTO_DOCUMENT_G;
/
CREATE OR REPLACE PACKAGE BODY HRM_AUTO_DOCUMENT_G
AS
/******************************************************************************/
/* Project      : FPCB ERP
/* Module       : HR
/* Program Name : HRM_AUTO_DOCUMENT_G
/* Description  : 인사 자동분개유형등록 관리.
/*
/* Reference by :
/* Program History :
/*------------------------------------------------------------------------------
/*   Date       In Charge          Description
/*------------------------------------------------------------------------------
/* 07-JUN-2010  Jeon Ho Su          Initialize
/******************************************************************************/
-- 자동분개유형 헤더 조회.
  PROCEDURE SELECT_AUTO_DOCUMENT_H
            ( P_CURSOR               OUT TYPES.TCURSOR
            , W_JOB_CODE             IN HRM_AUTO_DOCUMENT_HEADER.JOB_CODE%TYPE
            , W_SOB_ID               IN HRM_AUTO_DOCUMENT_HEADER.SOB_ID%TYPE
            , W_ORG_ID               IN HRM_AUTO_DOCUMENT_HEADER.ORG_ID%TYPE
            )
  AS
  BEGIN
    OPEN P_CURSOR FOR
      SELECT DH.DOCUMENT_HEADER_ID
           , FI_COMMON_G.CODE_NAME_F('SLIP_TYPE', DH.SLIP_TYPE, DH.SOB_ID) AS SLIP_TYPE_NAME
           , DH.SLIP_TYPE
           , DH.JOB_CODE
           , DH.JOB_DESC
           , DH.REMARK
           , DH.ENABLED_FLAG
           , DH.EFFECTIVE_DATE_FR
           , DH.EFFECTIVE_DATE_TO
        FROM HRM_AUTO_DOCUMENT_HEADER DH
       WHERE DH.SOB_ID                  = W_SOB_ID
         AND DH.ORG_ID                  = W_ORG_ID
         AND DH.JOB_CODE                = W_JOB_CODE
      ORDER BY DH.SLIP_TYPE, DH.JOB_CODE
      ;
  END SELECT_AUTO_DOCUMENT_H;

-- 자동분개유형 헤더 삽입.
  PROCEDURE INSERT_AUTO_DOCUMENT_H
            ( P_DOCUMENT_HEADER_ID OUT HRM_AUTO_DOCUMENT_HEADER.DOCUMENT_HEADER_ID%TYPE
            , P_SOB_ID             IN HRM_AUTO_DOCUMENT_HEADER.SOB_ID%TYPE
            , P_ORG_ID             IN HRM_AUTO_DOCUMENT_HEADER.ORG_ID%TYPE
            , P_SLIP_TYPE          IN HRM_AUTO_DOCUMENT_HEADER.SLIP_TYPE%TYPE
            , P_JOB_CODE           IN HRM_AUTO_DOCUMENT_HEADER.JOB_CODE%TYPE
            , P_JOB_DESC           IN HRM_AUTO_DOCUMENT_HEADER.JOB_DESC%TYPE
            , P_REMARK             IN HRM_AUTO_DOCUMENT_HEADER.REMARK%TYPE
            , P_ENABLED_FLAG       IN HRM_AUTO_DOCUMENT_HEADER.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR  IN HRM_AUTO_DOCUMENT_HEADER.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO  IN HRM_AUTO_DOCUMENT_HEADER.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID            IN HRM_AUTO_DOCUMENT_HEADER.CREATED_BY%TYPE
            )
  AS
    V_SYSDATE   DATE := GET_LOCAL_DATE(P_SOB_ID);
    V_RECORD_COUNT NUMBER := 0;

  BEGIN
    -- 동일한 전표유형/업무분류코드,업무구분,차대,계정 ID존재 체크.
    BEGIN
      SELECT COUNT(DH.DOCUMENT_HEADER_ID) AS RECORD_COUNT
        INTO V_RECORD_COUNT
        FROM HRM_AUTO_DOCUMENT_HEADER DH
       WHERE DH.SOB_ID                  = P_SOB_ID
         AND DH.SLIP_TYPE               = P_SLIP_TYPE
         AND DH.JOB_CODE                = P_JOB_CODE
      ;
    EXCEPTION WHEN OTHERS THEN
      V_RECORD_COUNT := 0;
    END;
    IF V_RECORD_COUNT <> 0 THEN
      RAISE_APPLICATION_ERROR(ERRNUMS.Exist_Data_Code, '[' || P_JOB_CODE || ']' || ERRNUMS.Exist_Data_Desc);
      RETURN;
    END IF;

    SELECT HRM_AUTO_DOCUMENT_HEADER_S1.NEXTVAL
      INTO P_DOCUMENT_HEADER_ID
      FROM DUAL;

    INSERT INTO HRM_AUTO_DOCUMENT_HEADER
    ( DOCUMENT_HEADER_ID
    , SOB_ID 
    , ORG_ID 
    , SLIP_TYPE 
    , JOB_CODE 
    , JOB_DESC 
    , REMARK 
    , ENABLED_FLAG 
    , EFFECTIVE_DATE_FR 
    , EFFECTIVE_DATE_TO 
    , CREATION_DATE 
    , CREATED_BY 
    , LAST_UPDATE_DATE 
    , LAST_UPDATED_BY )
    VALUES
    ( P_DOCUMENT_HEADER_ID
    , P_SOB_ID
    , P_ORG_ID
    , P_SLIP_TYPE
    , P_JOB_CODE
    , P_JOB_DESC
    , P_REMARK
    , P_ENABLED_FLAG
    , P_EFFECTIVE_DATE_FR
    , P_EFFECTIVE_DATE_TO
    , V_SYSDATE
    , P_USER_ID
    , V_SYSDATE
    , P_USER_ID );
  END INSERT_AUTO_DOCUMENT_H;

-- 자동분개유형 헤더 수정.
  PROCEDURE UPDATE_AUTO_DOCUMENT_H
            ( W_DOCUMENT_HEADER_ID IN HRM_AUTO_DOCUMENT_HEADER.DOCUMENT_HEADER_ID%TYPE
            , P_SOB_ID             IN HRM_AUTO_DOCUMENT_HEADER.SOB_ID%TYPE
            , P_ORG_ID             IN HRM_AUTO_DOCUMENT_HEADER.ORG_ID%TYPE
            , P_JOB_DESC           IN HRM_AUTO_DOCUMENT_HEADER.JOB_DESC%TYPE
            , P_REMARK             IN HRM_AUTO_DOCUMENT_HEADER.REMARK%TYPE
            , P_ENABLED_FLAG       IN HRM_AUTO_DOCUMENT_HEADER.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR  IN HRM_AUTO_DOCUMENT_HEADER.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO  IN HRM_AUTO_DOCUMENT_HEADER.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID            IN HRM_AUTO_DOCUMENT_HEADER.CREATED_BY%TYPE
            )
  AS
    V_SYSDATE DATE := GET_LOCAL_DATE(P_SOB_ID);
  BEGIN
    UPDATE HRM_AUTO_DOCUMENT_HEADER
      SET JOB_DESC           = P_JOB_DESC
        , REMARK             = P_REMARK
        , ENABLED_FLAG       = P_ENABLED_FLAG
        , EFFECTIVE_DATE_FR  = P_EFFECTIVE_DATE_FR
        , EFFECTIVE_DATE_TO  = P_EFFECTIVE_DATE_TO
        , LAST_UPDATE_DATE   = V_SYSDATE
        , LAST_UPDATED_BY    = P_USER_ID
    WHERE DOCUMENT_HEADER_ID = W_DOCUMENT_HEADER_ID
    ;
  END UPDATE_AUTO_DOCUMENT_H;

---------------------------------------------------------------------------------------------------
-- 자동분개유형 라인 조회.
  PROCEDURE SELECT_AUTO_DOCUMENT_L
            ( P_CURSOR1              OUT TYPES.TCURSOR1
            , W_DOCUMENT_HEADER_ID   IN HRM_AUTO_DOCUMENT_LINE.DOCUMENT_HEADER_ID%TYPE
            , W_SOB_ID               IN HRM_AUTO_DOCUMENT_LINE.SOB_ID%TYPE
            , W_ORG_ID               IN HRM_AUTO_DOCUMENT_LINE.ORG_ID%TYPE
            )
  AS
  BEGIN
    OPEN P_CURSOR1 FOR
      SELECT DL.DOCUMENT_LINE_ID
           , DL.DOCUMENT_HEADER_ID
           , DL.LINE_SEQ
           , AC.ACCOUNT_CODE
           , AC.ACCOUNT_DESC
           , DL.ACCOUNT_CONTROL_ID
           , DL.ACCOUNT_DR_CR
           , FI_COMMON_G.CODE_NAME_F('ACCOUNT_DR_CR', DL.ACCOUNT_DR_CR, DL.SOB_ID) AS ACCOUNT_DR_CR_NAME          
           , DL.REMARK
           , DL.CONTRA_ACCOUNT_YN
           , DL.DIR_INDIR_TYPE
           , HRM_COMMON_G.CODE_NAME_F('DIR_INDIR_TYPE', DL.DIR_INDIR_TYPE, DL.SOB_ID, W_ORG_ID) AS DIR_INDIR_TYPE_DESC
           , DL.VENDOR_CODE
           , SC.SUPP_CUST_NAME AS VENDOR_DESC
           , DL.ENABLED_FLAG
           , DL.EFFECTIVE_DATE_FR
           , DL.EFFECTIVE_DATE_TO
        FROM HRM_AUTO_DOCUMENT_LINE DL
          , FI_ACCOUNT_CONTROL AC
          , FI_SUPP_CUST_V SC
       WHERE DL.ACCOUNT_CONTROL_ID      = AC.ACCOUNT_CONTROL_ID
         AND DL.VENDOR_CODE             = SC.SUPP_CUST_CODE(+)
         AND DL.SOB_ID                  = SC.SOB_ID(+)
         AND DL.SOB_ID                  = W_SOB_ID
         AND DL.ORG_ID                  = W_ORG_ID
         AND DL.DOCUMENT_HEADER_ID      = W_DOCUMENT_HEADER_ID
      ORDER BY DL.LINE_SEQ
      ;
  END SELECT_AUTO_DOCUMENT_L;

-- 자동분개유형 라인 삽입.
  PROCEDURE INSERT_AUTO_DOCUMENT_L
            ( P_DOCUMENT_LINE_ID   OUT HRM_AUTO_DOCUMENT_LINE.DOCUMENT_LINE_ID%TYPE
            , P_SOB_ID             IN HRM_AUTO_DOCUMENT_LINE.SOB_ID%TYPE
            , P_ORG_ID             IN HRM_AUTO_DOCUMENT_LINE.ORG_ID%TYPE
            , P_DOCUMENT_HEADER_ID IN HRM_AUTO_DOCUMENT_LINE.DOCUMENT_HEADER_ID%TYPE
            , P_ACCOUNT_CONTROL_ID IN HRM_AUTO_DOCUMENT_LINE.ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE       IN VARCHAR2
            , P_ACCOUNT_DR_CR      IN HRM_AUTO_DOCUMENT_LINE.ACCOUNT_DR_CR%TYPE
            , P_REMARK             IN HRM_AUTO_DOCUMENT_LINE.REMARK%TYPE
            , P_VAT_YN             IN HRM_AUTO_DOCUMENT_LINE.VAT_YN%TYPE
            , P_CONTRA_ACCOUNT_YN  IN HRM_AUTO_DOCUMENT_LINE.CONTRA_ACCOUNT_YN%TYPE
            , P_DIR_INDIR_TYPE     IN HRM_AUTO_DOCUMENT_LINE.DIR_INDIR_TYPE%TYPE
            , P_VENDOR_CODE        IN HRM_AUTO_DOCUMENT_LINE.VENDOR_CODE%TYPE
            , P_ENABLED_FLAG       IN HRM_AUTO_DOCUMENT_LINE.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR  IN HRM_AUTO_DOCUMENT_LINE.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO  IN HRM_AUTO_DOCUMENT_LINE.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID            IN HRM_AUTO_DOCUMENT_LINE.CREATED_BY%TYPE
            )
  AS
    V_SYSDATE   DATE := GET_LOCAL_DATE(P_SOB_ID);
    V_RECORD_COUNT NUMBER := 0;
    V_LINE_SEQ     NUMBER := 1;
  BEGIN
    -- 동일한 전표유형/업무분류코드,업무구분,차대,계정 ID존재 체크.
    BEGIN
      SELECT COUNT(DL.DOCUMENT_LINE_ID) AS RECORD_COUNT
        INTO V_RECORD_COUNT
        FROM HRM_AUTO_DOCUMENT_LINE DL
       WHERE DL.SOB_ID                  = P_SOB_ID
         AND DL.ORG_ID                  = P_ORG_ID
         AND DL.DOCUMENT_HEADER_ID      = P_DOCUMENT_HEADER_ID
         AND DL.ACCOUNT_CONTROL_ID      = P_ACCOUNT_CONTROL_ID
      ;
    EXCEPTION WHEN OTHERS THEN
      V_RECORD_COUNT := 0;
    END;
    IF V_RECORD_COUNT <> 0 THEN
      RAISE_APPLICATION_ERROR(ERRNUMS.Exist_Data_Code, '[' || P_ACCOUNT_CODE || ']' || ERRNUMS.Exist_Data_Desc);
      RETURN;
    END IF;
    
    -- 상대계정 중복 검증(1개만 존재해야 함).
    BEGIN
      SELECT COUNT(DL.DOCUMENT_LINE_ID) AS RECORD_COUNT
        INTO V_RECORD_COUNT
        FROM HRM_AUTO_DOCUMENT_LINE DL
       WHERE DL.SOB_ID                  = P_SOB_ID
         AND DL.ORG_ID                  = P_ORG_ID
         AND DL.DOCUMENT_HEADER_ID      = P_DOCUMENT_HEADER_ID
         AND DL.CONTRA_ACCOUNT_YN       = 'Y'
      ;
    EXCEPTION WHEN OTHERS THEN
      V_RECORD_COUNT := 0;
    END;
    IF V_RECORD_COUNT <> 0 THEN
      RAISE_APPLICATION_ERROR(-20001, EAPP_MESSAGE_G.RETURN_TEXT_F(USERENV_G.GET_TERRITORY_S_F, 'FCM_10415'));
      RETURN;
    END IF;

    SELECT HRM_AUTO_DOCUMENT_LINE_S1.NEXTVAL
      INTO P_DOCUMENT_LINE_ID
      FROM DUAL;

    -- SEQ 생성.
    BEGIN
      SELECT CASE
               WHEN P_CONTRA_ACCOUNT_YN = 'Y' THEN 99
               ELSE NVL(MAX(DL.LINE_SEQ), 0) + 1 
             END AS LINE_SEQ
        INTO V_LINE_SEQ
        FROM HRM_AUTO_DOCUMENT_LINE DL
       WHERE DL.SOB_ID                  = P_SOB_ID
         AND DL.ORG_ID                  = P_ORG_ID
         AND DL.DOCUMENT_HEADER_ID      = P_DOCUMENT_HEADER_ID
      ;
    EXCEPTION WHEN OTHERS THEN
      V_LINE_SEQ := 1;
    END;

    INSERT INTO HRM_AUTO_DOCUMENT_LINE
    ( DOCUMENT_LINE_ID
    , SOB_ID 
    , ORG_ID 
    , DOCUMENT_HEADER_ID 
    , LINE_SEQ 
    , ACCOUNT_CONTROL_ID 
    , ACCOUNT_DR_CR 
    , REMARK 
    , VAT_YN 
    , CONTRA_ACCOUNT_YN 
    , DIR_INDIR_TYPE 
    , VENDOR_CODE 
    , ENABLED_FLAG 
    , EFFECTIVE_DATE_FR 
    , EFFECTIVE_DATE_TO 
    , CREATION_DATE 
    , CREATED_BY 
    , LAST_UPDATE_DATE 
    , LAST_UPDATED_BY )
    VALUES
    ( P_DOCUMENT_LINE_ID
    , P_SOB_ID
    , P_ORG_ID
    , P_DOCUMENT_HEADER_ID
    , V_LINE_SEQ
    , P_ACCOUNT_CONTROL_ID
    , P_ACCOUNT_DR_CR
    , P_REMARK
    , P_VAT_YN
    , P_CONTRA_ACCOUNT_YN
    , P_DIR_INDIR_TYPE
    , P_VENDOR_CODE
    , P_ENABLED_FLAG
    , P_EFFECTIVE_DATE_FR
    , P_EFFECTIVE_DATE_TO
    , V_SYSDATE
    , P_USER_ID
    , V_SYSDATE
    , P_USER_ID );
  END INSERT_AUTO_DOCUMENT_L;

-- 자동분개유형 라인 수정.
  PROCEDURE UPDATE_AUTO_DOCUMENT_L
            ( W_DOCUMENT_LINE_ID   IN HRM_AUTO_DOCUMENT_LINE.DOCUMENT_LINE_ID%TYPE
            , P_SOB_ID             IN HRM_AUTO_DOCUMENT_LINE.SOB_ID%TYPE
            , P_ORG_ID             IN HRM_AUTO_DOCUMENT_LINE.ORG_ID%TYPE
            , P_ACCOUNT_CONTROL_ID IN HRM_AUTO_DOCUMENT_LINE.ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE       IN VARCHAR2
            , P_ACCOUNT_DR_CR      IN HRM_AUTO_DOCUMENT_LINE.ACCOUNT_DR_CR%TYPE
            , P_REMARK             IN HRM_AUTO_DOCUMENT_LINE.REMARK%TYPE
            , P_VAT_YN             IN HRM_AUTO_DOCUMENT_LINE.VAT_YN%TYPE
            , P_CONTRA_ACCOUNT_YN  IN HRM_AUTO_DOCUMENT_LINE.CONTRA_ACCOUNT_YN%TYPE
            , P_DIR_INDIR_TYPE     IN HRM_AUTO_DOCUMENT_LINE.DIR_INDIR_TYPE%TYPE
            , P_VENDOR_CODE        IN HRM_AUTO_DOCUMENT_LINE.VENDOR_CODE%TYPE
            , P_ENABLED_FLAG       IN HRM_AUTO_DOCUMENT_LINE.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR  IN HRM_AUTO_DOCUMENT_LINE.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO  IN HRM_AUTO_DOCUMENT_LINE.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID            IN HRM_AUTO_DOCUMENT_LINE.CREATED_BY%TYPE 
            )
  AS
    V_SYSDATE DATE := GET_LOCAL_DATE(P_SOB_ID);
    V_RECORD_COUNT NUMBER := 0;
    V_LINE_SEQ     NUMBER := 1;
  BEGIN
    -- 동일한 전표유형/업무분류코드,업무구분,차대,계정 ID존재 체크.
    BEGIN
      SELECT COUNT(DL.DOCUMENT_LINE_ID) AS RECORD_COUNT
        INTO V_RECORD_COUNT
        FROM HRM_AUTO_DOCUMENT_LINE DL
       WHERE DL.SOB_ID                  = P_SOB_ID
         AND DL.ORG_ID                  = P_ORG_ID
         AND DL.DOCUMENT_LINE_ID        <> W_DOCUMENT_LINE_ID
         AND EXISTS
               (SELECT 'X'
                  FROM HRM_AUTO_DOCUMENT_LINE SDL
                WHERE SDL.DOCUMENT_HEADER_ID  = DL.DOCUMENT_HEADER_ID
                  AND SDL.DOCUMENT_LINE_ID    = W_DOCUMENT_LINE_ID
               )
         AND DL.ACCOUNT_CONTROL_ID      = P_ACCOUNT_CONTROL_ID
      ;
    EXCEPTION WHEN OTHERS THEN
      V_RECORD_COUNT := 0;
    END;
    IF V_RECORD_COUNT <> 0 THEN
      RAISE_APPLICATION_ERROR(ERRNUMS.Exist_Data_Code, '[' || P_ACCOUNT_CODE || ']' || ERRNUMS.Exist_Data_Desc);
      RETURN;
    END IF;
    
    -- 상대계정 중복 검증(1개만 존재해야 함).
    BEGIN
      SELECT COUNT(DL.DOCUMENT_LINE_ID) AS RECORD_COUNT
        INTO V_RECORD_COUNT
        FROM HRM_AUTO_DOCUMENT_LINE DL
       WHERE DL.SOB_ID                  = P_SOB_ID
         AND DL.ORG_ID                  = P_ORG_ID
         AND DL.CONTRA_ACCOUNT_YN       = 'Y'
         AND DL.DOCUMENT_LINE_ID        <> W_DOCUMENT_LINE_ID
         AND EXISTS
               (SELECT 'X'
                  FROM HRM_AUTO_DOCUMENT_LINE SDL
                WHERE SDL.DOCUMENT_HEADER_ID  = DL.DOCUMENT_HEADER_ID
                  AND SDL.DOCUMENT_LINE_ID    = W_DOCUMENT_LINE_ID
               )
      ;
    EXCEPTION WHEN OTHERS THEN
      V_RECORD_COUNT := 0;
    END;
    IF V_RECORD_COUNT <> 0 THEN
      RAISE_APPLICATION_ERROR(-20001, EAPP_MESSAGE_G.RETURN_TEXT_F(USERENV_G.GET_TERRITORY_S_F, 'FCM_10415'));
      RETURN;
    END IF;

    -- SEQ 생성.
    BEGIN
      SELECT CASE
               WHEN P_CONTRA_ACCOUNT_YN = 'Y' THEN 99
               ELSE DL.LINE_SEQ 
             END AS LINE_SEQ
        INTO V_LINE_SEQ
        FROM HRM_AUTO_DOCUMENT_LINE DL
       WHERE DL.SOB_ID                  = P_SOB_ID
         AND DL.ORG_ID                  = P_ORG_ID
         AND DL.DOCUMENT_LINE_ID        = W_DOCUMENT_LINE_ID
      ;
    EXCEPTION WHEN OTHERS THEN
      V_LINE_SEQ := 1;
    END;
    
    UPDATE HRM_AUTO_DOCUMENT_LINE
      SET LINE_SEQ           = V_LINE_SEQ
        , ACCOUNT_CONTROL_ID = P_ACCOUNT_CONTROL_ID
        , ACCOUNT_DR_CR      = P_ACCOUNT_DR_CR
        , REMARK             = P_REMARK
        , VAT_YN             = P_VAT_YN
        , CONTRA_ACCOUNT_YN  = P_CONTRA_ACCOUNT_YN
        , DIR_INDIR_TYPE     = P_DIR_INDIR_TYPE
        , VENDOR_CODE        = P_VENDOR_CODE
        , ENABLED_FLAG       = P_ENABLED_FLAG
        , EFFECTIVE_DATE_FR  = P_EFFECTIVE_DATE_FR
        , EFFECTIVE_DATE_TO  = P_EFFECTIVE_DATE_TO
        , LAST_UPDATE_DATE   = V_SYSDATE
        , LAST_UPDATED_BY    = P_USER_ID
    WHERE DOCUMENT_LINE_ID   = W_DOCUMENT_LINE_ID;
  END UPDATE_AUTO_DOCUMENT_L;

-- 자동분개유형 라인 삭제.
  PROCEDURE DELETE_AUTO_DOCUMENT_L
            ( W_DOCUMENT_LINE_ID   IN HRM_AUTO_DOCUMENT_LINE.DOCUMENT_LINE_ID%TYPE
            , P_SOB_ID             IN HRM_AUTO_DOCUMENT_LINE.SOB_ID%TYPE
            , P_ORG_ID             IN HRM_AUTO_DOCUMENT_LINE.ORG_ID%TYPE
            )
  AS
    V_RECORD_COUNT NUMBER := 0;
    V_LINE_SEQ     NUMBER := 1;
  BEGIN
    -- 수당 항목 존재 체크.
    BEGIN
      SELECT COUNT(DA.DOCUMENT_LINE_ID) AS RECORD_COUNT
        INTO V_RECORD_COUNT
        FROM HRM_AUTO_DOCUMENT_ALLOWANCE DA
       WHERE DA.DOCUMENT_LINE_ID        = W_DOCUMENT_LINE_ID
      ;
    EXCEPTION WHEN OTHERS THEN
      V_RECORD_COUNT := 0;
    END;
    IF V_RECORD_COUNT <> 0 THEN
      RAISE_APPLICATION_ERROR(-20001, EAPP_MESSAGE_G.RETURN_TEXT_F(USERENV_G.GET_TERRITORY_S_F, 'EAPP_10016'));
      RETURN;
    END IF;

    DELETE FROM HRM_AUTO_DOCUMENT_LINE
    WHERE DOCUMENT_LINE_ID   = W_DOCUMENT_LINE_ID;
  END DELETE_AUTO_DOCUMENT_L;
  
---------------------------------------------------------------------------------------------------
-- 자동분개유형 급상여 지급/공제 항목 조회.
  PROCEDURE SELECT_AUTO_DOCUMENT_ALLOWANCE
            ( P_CURSOR1              OUT TYPES.TCURSOR1
            , W_DOCUMENT_LINE_ID     IN HRM_AUTO_DOCUMENT_ALLOWANCE.DOCUMENT_LINE_ID%TYPE
            , W_SOB_ID               IN HRM_AUTO_DOCUMENT_LINE.SOB_ID%TYPE
            , W_ORG_ID               IN HRM_AUTO_DOCUMENT_LINE.ORG_ID%TYPE
            )
  AS
  BEGIN
    OPEN P_CURSOR1 FOR
      SELECT 
            DA.DOCUMENT_ALLOWANCE_ID
          , DA.DOCUMENT_LINE_ID
          , DA.ALLOWANCE_TYPE
          , DA.ALLOWANCE_ID
          , SX1.ALLOWANCE_NAME
          , SX1.ALLOWANCE_CODE                
        FROM HRM_AUTO_DOCUMENT_ALLOWANCE DA
          , ( SELECT HC.COMMON_ID AS ALLOWANCE_ID
                  , HC.CODE       AS ALLOWANCE_CODE
                  , HC.CODE_NAME  AS ALLOWANCE_NAME
                  , HC.SORT_NUM
                FROM HRM_COMMON HC
              WHERE HC.GROUP_CODE   IN('ALLOWANCE', 'DEDUCTION')
                AND HC.SOB_ID     = W_SOB_ID
                AND HC.ORG_ID       = W_ORG_ID
            ) SX1
       WHERE DA.ALLOWANCE_ID        = SX1.ALLOWANCE_ID
         AND DA.DOCUMENT_LINE_ID    = W_DOCUMENT_LINE_ID
      ORDER BY SX1.ALLOWANCE_CODE, SX1.SORT_NUM
      ;
  END SELECT_AUTO_DOCUMENT_ALLOWANCE;

-- 자동분개유형 급상여 지급/공제 항목 삽입.
  PROCEDURE INSERT_DOCUMENT_ALLOWANCE
            ( P_DOCUMENT_ALLOWANCE_ID OUT HRM_AUTO_DOCUMENT_ALLOWANCE.DOCUMENT_ALLOWANCE_ID%TYPE
            , P_SOB_ID                IN NUMBER
            , P_DOCUMENT_LINE_ID      IN HRM_AUTO_DOCUMENT_ALLOWANCE.DOCUMENT_LINE_ID%TYPE
            , P_ALLOWANCE_TYPE        OUT HRM_AUTO_DOCUMENT_ALLOWANCE.ALLOWANCE_TYPE%TYPE
            , P_ALLOWANCE_ID          IN HRM_AUTO_DOCUMENT_ALLOWANCE.ALLOWANCE_ID%TYPE
            , P_ALLOWANCE_CODE        IN VARCHAR2            
            , P_USER_ID               IN HRM_AUTO_DOCUMENT_ALLOWANCE.CREATED_BY%TYPE
            )
  AS
    V_SYSDATE   DATE := GET_LOCAL_DATE(P_SOB_ID);
    V_RECORD_COUNT NUMBER := 0;
  BEGIN
    BEGIN
      SELECT COUNT(DA.DOCUMENT_ALLOWANCE_ID) AS RECORD_COUNT
        INTO V_RECORD_COUNT                
        FROM HRM_AUTO_DOCUMENT_ALLOWANCE DA
      WHERE DA.DOCUMENT_LINE_ID    = P_DOCUMENT_LINE_ID
        AND DA.ALLOWANCE_ID        = P_ALLOWANCE_ID
      ;
    EXCEPTION WHEN OTHERS THEN
      V_RECORD_COUNT := 0; 
    END;
    IF V_RECORD_COUNT <> 0 THEN
      RAISE_APPLICATION_ERROR(ERRNUMS.Exist_Data_Code, '[' || P_ALLOWANCE_CODE || ']' || ERRNUMS.Exist_Data_Desc);
      RETURN;
    END IF;
    
    P_ALLOWANCE_TYPE := SUBSTR(P_ALLOWANCE_CODE, 1, 1);
    SELECT HRM_AUTO_DOCUMENT_ALLOWANCE_S1.NEXTVAL
      INTO P_DOCUMENT_ALLOWANCE_ID
      FROM DUAL;

    INSERT INTO HRM_AUTO_DOCUMENT_ALLOWANCE
    ( DOCUMENT_ALLOWANCE_ID
    , DOCUMENT_LINE_ID 
    , ALLOWANCE_TYPE 
    , ALLOWANCE_ID 
    , CREATION_DATE 
    , CREATED_BY 
    , LAST_UPDATE_DATE 
    , LAST_UPDATED_BY )
    VALUES
    ( P_DOCUMENT_ALLOWANCE_ID
    , P_DOCUMENT_LINE_ID
    , P_ALLOWANCE_TYPE
    , P_ALLOWANCE_ID
    , V_SYSDATE
    , P_USER_ID
    , V_SYSDATE
    , P_USER_ID );     
  END INSERT_DOCUMENT_ALLOWANCE;

-- 자동분개유형 급상여 지급/공제 항목 수정.
  PROCEDURE UPDATE_DOCUMENT_ALLOWANCE
            ( W_DOCUMENT_ALLOWANCE_ID  IN HRM_AUTO_DOCUMENT_ALLOWANCE.DOCUMENT_ALLOWANCE_ID%TYPE
             , P_SOB_ID                IN NUMBER
             , P_DOCUMENT_LINE_ID      IN HRM_AUTO_DOCUMENT_ALLOWANCE.DOCUMENT_LINE_ID%TYPE
             , P_ALLOWANCE_TYPE        OUT HRM_AUTO_DOCUMENT_ALLOWANCE.ALLOWANCE_TYPE%TYPE
             , P_ALLOWANCE_ID          IN HRM_AUTO_DOCUMENT_ALLOWANCE.ALLOWANCE_ID%TYPE
             , P_ALLOWANCE_CODE        IN VARCHAR2
             , P_USER_ID               IN HRM_AUTO_DOCUMENT_ALLOWANCE.CREATED_BY%TYPE
            )
  AS
    V_SYSDATE   DATE := GET_LOCAL_DATE(P_SOB_ID);
    V_RECORD_COUNT NUMBER := 0;
  BEGIN
    BEGIN
      SELECT COUNT(DA.DOCUMENT_ALLOWANCE_ID) AS RECORD_COUNT
        INTO V_RECORD_COUNT                
        FROM HRM_AUTO_DOCUMENT_ALLOWANCE DA
      WHERE DA.DOCUMENT_LINE_ID    = P_DOCUMENT_LINE_ID
        AND DA.ALLOWANCE_ID        = P_ALLOWANCE_ID
      ;
    EXCEPTION WHEN OTHERS THEN
      V_RECORD_COUNT := 0; 
    END;
    IF V_RECORD_COUNT <> 0 THEN
      RAISE_APPLICATION_ERROR(ERRNUMS.Exist_Data_Code, '[' || P_ALLOWANCE_CODE || ']' || ERRNUMS.Exist_Data_Desc);
      RETURN;
    END IF;
    
    P_ALLOWANCE_TYPE := SUBSTR(P_ALLOWANCE_CODE, 1, 1);
    UPDATE HRM_AUTO_DOCUMENT_ALLOWANCE
      SET ALLOWANCE_TYPE        = P_ALLOWANCE_TYPE
        , ALLOWANCE_ID          = P_ALLOWANCE_ID
        , LAST_UPDATE_DATE      = V_SYSDATE
        , LAST_UPDATED_BY       = P_USER_ID
    WHERE DOCUMENT_ALLOWANCE_ID = W_DOCUMENT_ALLOWANCE_ID;
  END UPDATE_DOCUMENT_ALLOWANCE;

-- 자동분개유형 급상여 지급/공제 항목 삭제.
  PROCEDURE DELETE_DOCUMENT_ALLOWANCE
            ( W_DOCUMENT_ALLOWANCE_ID  IN HRM_AUTO_DOCUMENT_ALLOWANCE.DOCUMENT_ALLOWANCE_ID%TYPE
            )
  AS
  BEGIN
    DELETE FROM HRM_AUTO_DOCUMENT_ALLOWANCE
    WHERE DOCUMENT_ALLOWANCE_ID = W_DOCUMENT_ALLOWANCE_ID;
  END DELETE_DOCUMENT_ALLOWANCE;
  
---------------------------------------------------------------------------------------------------
-- 자동분개유형 업무구분 조회.
  PROCEDURE LU_JOB_CODE
            ( P_CURSOR3              OUT TYPES.TCURSOR3
            , W_SOB_ID               IN HRM_AUTO_DOCUMENT_HEADER.SOB_ID%TYPE
            , W_SLIP_TYPE            IN HRM_AUTO_DOCUMENT_HEADER.SLIP_TYPE%TYPE
            , W_ENABLED_YN           IN HRM_AUTO_DOCUMENT_HEADER.ENABLED_FLAG%TYPE
            )
  AS
    V_SYSDATE                        DATE := NULL;
  BEGIN
    IF W_ENABLED_YN = 'Y' THEN
      V_SYSDATE := NULL;
    ELSE
      V_SYSDATE := GET_LOCAL_DATE(W_SOB_ID);
    END IF;

    OPEN P_CURSOR3 FOR
      SELECT DH.JOB_DESC
           , DH.JOB_CODE
           , DH.DOCUMENT_HEADER_ID
        FROM HRM_AUTO_DOCUMENT_HEADER DH
       WHERE DH.SOB_ID           = W_SOB_ID
         AND DH.SLIP_TYPE        = NVL(W_SLIP_TYPE, DH.SLIP_TYPE)
         AND DH.ENABLED_FLAG     = DECODE(W_ENABLED_YN, 'Y', 'Y', DH.ENABLED_FLAG)
         AND DH.EFFECTIVE_DATE_FR  <= NVL(V_SYSDATE, DH.EFFECTIVE_DATE_FR)
         AND (DH.EFFECTIVE_DATE_TO IS NULL OR DH.EFFECTIVE_DATE_TO  >= NVL(V_SYSDATE, DH.EFFECTIVE_DATE_TO))
     ORDER BY DH.JOB_CODE
    ;
  END LU_JOB_CODE;

END HRM_AUTO_DOCUMENT_G;
/
