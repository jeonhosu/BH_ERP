CREATE OR REPLACE PACKAGE FI_ASSET_CATEGORY_G
AS

-- 자산카테고리 조회.
  PROCEDURE SELECT_ASSET_CATEGORY
            ( P_CURSOR               OUT TYPES.TCURSOR
            , W_ASSET_CATEGORY_ID    IN FI_ASSET_MASTER.ASSET_CATEGORY_ID%TYPE
            , W_SOB_ID               IN FI_ASSET_MASTER.SOB_ID%TYPE
            );

-- 자산카테고리 삽입.
  PROCEDURE INSERT_ASSET_CATEGORY
            ( P_ASSET_CATEGORY_ID    OUT FI_ASSET_CATEGORY.ASSET_CATEGORY_ID%TYPE
            , P_ASSET_CATEGORY_CODE  IN FI_ASSET_CATEGORY.ASSET_CATEGORY_CODE%TYPE
            , P_ASSET_CATEGORY_NAME  IN FI_ASSET_CATEGORY.ASSET_CATEGORY_NAME%TYPE
            , P_SOB_ID               IN FI_ASSET_CATEGORY.SOB_ID%TYPE
            , P_EXPENSE_TYPE         IN FI_ASSET_CATEGORY.EXPENSE_TYPE%TYPE
            , P_ASSET_TYPE           IN FI_ASSET_CATEGORY.ASSET_TYPE%TYPE
            , P_DPR_METHOD_TYPE      IN FI_ASSET_CATEGORY.DPR_METHOD_TYPE%TYPE
            , P_PROGRESS_YEAR        IN FI_ASSET_CATEGORY.PROGRESS_YEAR%TYPE
            , P_RESIDUAL_AMOUNT      IN FI_ASSET_CATEGORY.RESIDUAL_AMOUNT%TYPE
            , P_IFRS_DPR_METHOD_TYPE IN FI_ASSET_CATEGORY.IFRS_DPR_METHOD_TYPE%TYPE
            , P_IFRS_PROGRESS_YEAR   IN FI_ASSET_CATEGORY.IFRS_PROGRESS_YEAR%TYPE
            , P_IFRS_RESIDUAL_AMOUNT IN FI_ASSET_CATEGORY.IFRS_RESIDUAL_AMOUNT%TYPE
            , P_ACCOUNT_CONTROL_ID   IN FI_ASSET_CATEGORY.AST_ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE         IN FI_ASSET_CATEGORY.AST_ACCOUNT_CODE%TYPE
            , P_DPR_ACCOUNT_CONTROL_ID   IN FI_ASSET_CATEGORY.DPR_ACCOUNT_CONTROL_ID%TYPE
            , P_DPR_ACCOUNT_CODE         IN FI_ASSET_CATEGORY.DPR_ACCOUNT_CODE%TYPE
            , P_DPR_SUM_ACCOUNT_CONTROL_ID   IN FI_ASSET_CATEGORY.DPR_SUM_ACCOUNT_CONTROL_ID%TYPE
            , P_DPR_SUM_ACCOUNT_CODE         IN FI_ASSET_CATEGORY.DPR_SUM_ACCOUNT_CODE%TYPE
            , P_REMARK               IN FI_ASSET_CATEGORY.REMARK%TYPE
            , P_ENABLED_FLAG         IN FI_ASSET_CATEGORY.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR    IN FI_ASSET_CATEGORY.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO    IN FI_ASSET_CATEGORY.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID              IN FI_ASSET_CATEGORY.CREATED_BY%TYPE
            );

-- 자산카테고리 수정.
  PROCEDURE UPDATE_ASSET_CATEGORY
            ( W_ASSET_CATEGORY_ID    IN FI_ASSET_CATEGORY.ASSET_CATEGORY_ID%TYPE
            , P_ASSET_CATEGORY_NAME  IN FI_ASSET_CATEGORY.ASSET_CATEGORY_NAME%TYPE
            , P_EXPENSE_TYPE         IN FI_ASSET_CATEGORY.EXPENSE_TYPE%TYPE
            , P_ASSET_TYPE           IN FI_ASSET_CATEGORY.ASSET_TYPE%TYPE
            , P_DPR_METHOD_TYPE      IN FI_ASSET_CATEGORY.DPR_METHOD_TYPE%TYPE
            , P_PROGRESS_YEAR        IN FI_ASSET_CATEGORY.PROGRESS_YEAR%TYPE
            , P_RESIDUAL_AMOUNT      IN FI_ASSET_CATEGORY.RESIDUAL_AMOUNT%TYPE
            , P_IFRS_DPR_METHOD_TYPE IN FI_ASSET_CATEGORY.IFRS_DPR_METHOD_TYPE%TYPE
            , P_IFRS_PROGRESS_YEAR   IN FI_ASSET_CATEGORY.IFRS_PROGRESS_YEAR%TYPE
            , P_IFRS_RESIDUAL_AMOUNT IN FI_ASSET_CATEGORY.IFRS_RESIDUAL_AMOUNT%TYPE
            , P_ACCOUNT_CONTROL_ID   IN FI_ASSET_CATEGORY.AST_ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE         IN FI_ASSET_CATEGORY.AST_ACCOUNT_CODE%TYPE
            , P_DPR_ACCOUNT_CONTROL_ID   IN FI_ASSET_CATEGORY.DPR_ACCOUNT_CONTROL_ID%TYPE
            , P_DPR_ACCOUNT_CODE         IN FI_ASSET_CATEGORY.DPR_ACCOUNT_CODE%TYPE
            , P_DPR_SUM_ACCOUNT_CONTROL_ID   IN FI_ASSET_CATEGORY.DPR_SUM_ACCOUNT_CONTROL_ID%TYPE
            , P_DPR_SUM_ACCOUNT_CODE         IN FI_ASSET_CATEGORY.DPR_SUM_ACCOUNT_CODE%TYPE
            , P_REMARK               IN FI_ASSET_CATEGORY.REMARK%TYPE
            , P_ENABLED_FLAG         IN FI_ASSET_CATEGORY.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR    IN FI_ASSET_CATEGORY.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO    IN FI_ASSET_CATEGORY.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID              IN FI_ASSET_CATEGORY.CREATED_BY%TYPE
            );

-- 자산카테고리 LOOKUP 조회.
  PROCEDURE LU_ASSET_CATEGORY
            ( P_CURSOR3              OUT TYPES.TCURSOR3
            , W_SOB_ID               IN FI_BANK.SOB_ID%TYPE
            , W_ENABLED_YN           IN FI_BANK.ENABLED_FLAG%TYPE
            );







--하기 부터가 임동언이 개발한 내용이다.(2011.10.18)



--조회
PROCEDURE LIST_ASSET_CATEGORY( 
      P_CURSOR  OUT TYPES.TCURSOR
    , W_SOB_ID  IN  FI_ASSET_CATEGORY.SOB_ID%TYPE
);









--신규자료 등록
PROCEDURE INS_ASSET_CATEGORY( 
      P_SOB_ID                  IN  FI_ASSET_CATEGORY.SOB_ID%TYPE               --회사아이디
    , P_ASSET_CATEGORY_CODE     IN  FI_ASSET_CATEGORY.ASSET_CATEGORY_CODE%TYPE  --자산유형코드      
    , P_ASSET_CATEGORY_NAME     IN  FI_ASSET_CATEGORY.ASSET_CATEGORY_NAME%TYPE  --자산유형명칭
    , P_ASSET_TYPE              IN  FI_ASSET_CATEGORY.ASSET_TYPE%TYPE           --자산형태(1:유형자산, 2:무형자산)
    , P_IFRS_DPR_METHOD_TYPE    IN  FI_ASSET_CATEGORY.IFRS_DPR_METHOD_TYPE%TYPE --(IFRS)감가상각방법 1:정액,2:정율
    , P_IFRS_PROGRESS_YEAR      IN  FI_ASSET_CATEGORY.IFRS_PROGRESS_YEAR%TYPE   --(IFRS)내용년수
    , P_IFRS_RESIDUAL_AMOUNT    IN  FI_ASSET_CATEGORY.IFRS_RESIDUAL_AMOUNT%TYPE --(IFRS)잔존가치
    , P_REMARK                  IN  FI_ASSET_CATEGORY.REMARK%TYPE               --비고

    , P_CREATED_BY              IN FI_ASSET_CATEGORY.CREATED_BY%TYPE            --생성자
);










--수정
PROCEDURE UPD_ASSET_CATEGORY( 
      W_SOB_ID                  IN  FI_ASSET_CATEGORY.SOB_ID%TYPE                --회사아이디
    , W_ASSET_CATEGORY_CODE     IN  FI_ASSET_CATEGORY.ASSET_CATEGORY_CODE%TYPE   --자산구분코드
      
    , P_ASSET_CATEGORY_NAME     IN  FI_ASSET_CATEGORY.ASSET_CATEGORY_NAME%TYPE  --자산구분명칭
    , P_ASSET_TYPE              IN  FI_ASSET_CATEGORY.ASSET_TYPE%TYPE           --자산형태(1:유형자산, 2:무형자산)
    , P_IFRS_DPR_METHOD_TYPE    IN  FI_ASSET_CATEGORY.IFRS_DPR_METHOD_TYPE%TYPE --IFRS_감가상각방법 1:정액,2:정율
    , P_IFRS_PROGRESS_YEAR      IN  FI_ASSET_CATEGORY.IFRS_PROGRESS_YEAR%TYPE   --IFRS_내용년수
    , P_IFRS_RESIDUAL_AMOUNT    IN  FI_ASSET_CATEGORY.IFRS_RESIDUAL_AMOUNT%TYPE --IFRS_잔존가치
    , P_REMARK                  IN  FI_ASSET_CATEGORY.REMARK%TYPE               --비고

    , P_LAST_UPDATED_BY         IN FI_ASSET_CATEGORY.LAST_UPDATED_BY%TYPE       --최종수정자
);








--삭제
PROCEDURE DEL_ASSET_CATEGORY(
      W_SOB_ID                  IN FI_ASSET_CATEGORY.SOB_ID%TYPE                --회사아이디
    , W_ASSET_CATEGORY_CODE     IN FI_ASSET_CATEGORY.ASSET_CATEGORY_CODE%TYPE   --자산구분코드
);







--자산유형 POPUP
PROCEDURE POPUP_ASSET_CATEGORY( 
      P_CURSOR  OUT TYPES.TCURSOR
    , W_SOB_ID  IN  FI_ASSET_CATEGORY.SOB_ID%TYPE
);






--자산유형명 반환
FUNCTION  F_ASSET_CATEGORY_NAME( 
    W_ASSET_CATEGORY_ID   IN   FI_ASSET_CATEGORY.ASSET_CATEGORY_ID%TYPE
) RETURN VARCHAR2;



END FI_ASSET_CATEGORY_G;
/
CREATE OR REPLACE PACKAGE BODY FI_ASSET_CATEGORY_G
AS
/******************************************************************************/
/* Project      : FPCB ERP
/* Module       : HR
/* Program Name : FI_ASSET_CATEGORY_G
/* Description  : 고정자산 카테고리 관리.
/*
/* Reference by :
/* Program History :
/*------------------------------------------------------------------------------
/*   Date       In Charge          Description
/*------------------------------------------------------------------------------
/* 07-JUN-2010  Jeon Ho Su          Initialize
/******************************************************************************/
-- 자산카테고리 조회.
  PROCEDURE SELECT_ASSET_CATEGORY
            ( P_CURSOR               OUT TYPES.TCURSOR
            , W_ASSET_CATEGORY_ID    IN FI_ASSET_MASTER.ASSET_CATEGORY_ID%TYPE
            , W_SOB_ID               IN FI_ASSET_MASTER.SOB_ID%TYPE
            )
  AS
  BEGIN
    OPEN P_CURSOR FOR
      SELECT AC.ASSET_CATEGORY_ID
           , AC.ASSET_CATEGORY_CODE
           , AC.ASSET_CATEGORY_NAME
           , AC.SOB_ID
           , AC.EXPENSE_TYPE 
           , FI_COMMON_G.CODE_NAME_F('EXPENSE_TYPE', AC.EXPENSE_TYPE, AC.SOB_ID) AS EXPENSE_TYPE_NAME
           , AC.ASSET_TYPE
           , FI_COMMON_G.CODE_NAME_F('ASSET_TYPE', AC.ASSET_TYPE, AC.SOB_ID, NULL) AS ASSET_TYPE_NAME
           , AC.DPR_METHOD_TYPE
           , FI_COMMON_G.CODE_NAME_F('DPR_METHOD_TYPE', AC.DPR_METHOD_TYPE, AC.SOB_ID, NULL) AS DPR_METHOD_TYPE_NAME
           , AC.PROGRESS_YEAR
           , AC.RESIDUAL_AMOUNT
           , AC.IFRS_DPR_METHOD_TYPE
           , FI_COMMON_G.CODE_NAME_F('DPR_METHOD_TYPE', AC.IFRS_DPR_METHOD_TYPE, AC.SOB_ID, NULL) AS IFRS_DPR_METHOD_TYPE_NAME
           , AC.IFRS_PROGRESS_YEAR
           , AC.IFRS_RESIDUAL_AMOUNT
           , AC.AST_ACCOUNT_CONTROL_ID AS ACCOUNT_CONTROL_ID
           , A_AC.ACCOUNT_CODE
           , A_AC.ACCOUNT_DESC           
           , AC.DPR_ACCOUNT_CONTROL_ID
           , D_AC.ACCOUNT_CODE AS DPR_ACCOUNT_CODE
           , D_AC.ACCOUNT_DESC AS DPR_ACCOUNT_DESC           
           , AC.DPR_SUM_ACCOUNT_CONTROL_ID AS DPR_SUM_ACCOUNT_CONTROL_ID
           , DS_AC.ACCOUNT_CODE AS DPR_SUM_ACCOUNT_CODE
           , DS_AC.ACCOUNT_DESC AS DPR_SUM_ACCOUNT_DESC           
           , AC.REMARK
           , AC.ENABLED_FLAG
           , AC.EFFECTIVE_DATE_FR
           , AC.EFFECTIVE_DATE_TO
        FROM FI_ASSET_CATEGORY AC
           , FI_ACCOUNT_CONTROL A_AC
           , FI_ACCOUNT_CONTROL D_AC
           , FI_ACCOUNT_CONTROL DS_AC
       WHERE AC.AST_ACCOUNT_CONTROL_ID  = A_AC.ACCOUNT_CONTROL_ID(+)
         AND AC.DPR_ACCOUNT_CONTROL_ID  = D_AC.ACCOUNT_CONTROL_ID(+)
         AND AC.DPR_SUM_ACCOUNT_CONTROL_ID  = DS_AC.ACCOUNT_CONTROL_ID(+)
         AND AC.ASSET_CATEGORY_ID           = NVL(W_ASSET_CATEGORY_ID, AC.ASSET_CATEGORY_ID)
         AND AC.SOB_ID                      = W_SOB_ID
      ORDER BY AC.ASSET_CATEGORY_CODE
      ;

  END SELECT_ASSET_CATEGORY;

-- 자산카테고리 삽입.
  PROCEDURE INSERT_ASSET_CATEGORY
            ( P_ASSET_CATEGORY_ID    OUT FI_ASSET_CATEGORY.ASSET_CATEGORY_ID%TYPE
            , P_ASSET_CATEGORY_CODE  IN FI_ASSET_CATEGORY.ASSET_CATEGORY_CODE%TYPE
            , P_ASSET_CATEGORY_NAME  IN FI_ASSET_CATEGORY.ASSET_CATEGORY_NAME%TYPE
            , P_SOB_ID               IN FI_ASSET_CATEGORY.SOB_ID%TYPE
            , P_EXPENSE_TYPE         IN FI_ASSET_CATEGORY.EXPENSE_TYPE%TYPE
            , P_ASSET_TYPE           IN FI_ASSET_CATEGORY.ASSET_TYPE%TYPE
            , P_DPR_METHOD_TYPE      IN FI_ASSET_CATEGORY.DPR_METHOD_TYPE%TYPE
            , P_PROGRESS_YEAR        IN FI_ASSET_CATEGORY.PROGRESS_YEAR%TYPE
            , P_RESIDUAL_AMOUNT      IN FI_ASSET_CATEGORY.RESIDUAL_AMOUNT%TYPE
            , P_IFRS_DPR_METHOD_TYPE IN FI_ASSET_CATEGORY.IFRS_DPR_METHOD_TYPE%TYPE
            , P_IFRS_PROGRESS_YEAR   IN FI_ASSET_CATEGORY.IFRS_PROGRESS_YEAR%TYPE
            , P_IFRS_RESIDUAL_AMOUNT IN FI_ASSET_CATEGORY.IFRS_RESIDUAL_AMOUNT%TYPE
            , P_ACCOUNT_CONTROL_ID   IN FI_ASSET_CATEGORY.AST_ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE         IN FI_ASSET_CATEGORY.AST_ACCOUNT_CODE%TYPE            
            , P_DPR_ACCOUNT_CONTROL_ID   IN FI_ASSET_CATEGORY.DPR_ACCOUNT_CONTROL_ID%TYPE
            , P_DPR_ACCOUNT_CODE         IN FI_ASSET_CATEGORY.DPR_ACCOUNT_CODE%TYPE
            , P_DPR_SUM_ACCOUNT_CONTROL_ID   IN FI_ASSET_CATEGORY.DPR_SUM_ACCOUNT_CONTROL_ID%TYPE
            , P_DPR_SUM_ACCOUNT_CODE         IN FI_ASSET_CATEGORY.DPR_SUM_ACCOUNT_CODE%TYPE            
            , P_REMARK               IN FI_ASSET_CATEGORY.REMARK%TYPE
            , P_ENABLED_FLAG         IN FI_ASSET_CATEGORY.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR    IN FI_ASSET_CATEGORY.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO    IN FI_ASSET_CATEGORY.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID              IN FI_ASSET_CATEGORY.CREATED_BY%TYPE
            )
  AS
    V_SYSDATE   DATE := GET_LOCAL_DATE(P_SOB_ID);
    V_RECORD_COUNT NUMBER := 0;

  BEGIN
    -- 동일한 은행 그룹코드 존재 체크.
    BEGIN
      SELECT COUNT(AC.ASSET_CATEGORY_ID) AS RECORD_COUNT
        INTO V_RECORD_COUNT
        FROM FI_ASSET_CATEGORY AC
       WHERE AC.ASSET_CATEGORY_CODE = P_ASSET_CATEGORY_CODE
         AND AC.SOB_ID              = P_SOB_ID
      ;
    EXCEPTION WHEN OTHERS THEN
      V_RECORD_COUNT := 0;
    END;
    IF V_RECORD_COUNT <> 0 THEN
      RAISE ERRNUMS.Exist_Data;
    END IF;

    SELECT FI_ASSET_CATEGORY_S1.NEXTVAL
       INTO P_ASSET_CATEGORY_ID
       FROM DUAL;

    INSERT INTO FI_ASSET_CATEGORY
    ( ASSET_CATEGORY_ID
    , ASSET_CATEGORY_CODE
    , ASSET_CATEGORY_NAME
    , SOB_ID
    , EXPENSE_TYPE
    , ASSET_TYPE
    , DPR_METHOD_TYPE
    , PROGRESS_YEAR
    , RESIDUAL_AMOUNT
    , IFRS_DPR_METHOD_TYPE
    , IFRS_PROGRESS_YEAR
    , IFRS_RESIDUAL_AMOUNT
    , AST_ACCOUNT_CONTROL_ID
    , AST_ACCOUNT_CODE
    , DPR_ACCOUNT_CONTROL_ID
    , DPR_ACCOUNT_CODE
    , DPR_SUM_ACCOUNT_CONTROL_ID
    , DPR_SUM_ACCOUNT_CODE
    , REMARK
    , ENABLED_FLAG
    , EFFECTIVE_DATE_FR
    , EFFECTIVE_DATE_TO
    , CREATION_DATE
    , CREATED_BY
    , LAST_UPDATE_DATE
    , LAST_UPDATED_BY )
    VALUES
    ( P_ASSET_CATEGORY_ID
    , P_ASSET_CATEGORY_CODE
    , P_ASSET_CATEGORY_NAME
    , P_SOB_ID
    , P_EXPENSE_TYPE
    , P_ASSET_TYPE
    , P_DPR_METHOD_TYPE
    , P_PROGRESS_YEAR
    , P_RESIDUAL_AMOUNT
    , P_IFRS_DPR_METHOD_TYPE
    , P_IFRS_PROGRESS_YEAR
    , P_IFRS_RESIDUAL_AMOUNT
    , P_ACCOUNT_CONTROL_ID
    , P_ACCOUNT_CODE
    , P_DPR_ACCOUNT_CONTROL_ID
    , P_DPR_ACCOUNT_CODE
    , P_DPR_SUM_ACCOUNT_CONTROL_ID
    , P_DPR_SUM_ACCOUNT_CODE
    , P_REMARK
    , P_ENABLED_FLAG
    , P_EFFECTIVE_DATE_FR
    , P_EFFECTIVE_DATE_TO
    , V_SYSDATE
    , P_USER_ID
    , V_SYSDATE
    , P_USER_ID );

  EXCEPTION
    WHEN ERRNUMS.Exist_Data THEN
    RAISE_APPLICATION_ERROR(ERRNUMS.Exist_Data_Code, ERRNUMS.Exist_Data_Desc);
  END INSERT_ASSET_CATEGORY;

-- 자산카테고리 수정.
  PROCEDURE UPDATE_ASSET_CATEGORY
            ( W_ASSET_CATEGORY_ID    IN FI_ASSET_CATEGORY.ASSET_CATEGORY_ID%TYPE
            , P_ASSET_CATEGORY_NAME  IN FI_ASSET_CATEGORY.ASSET_CATEGORY_NAME%TYPE
            , P_EXPENSE_TYPE         IN FI_ASSET_CATEGORY.EXPENSE_TYPE%TYPE
            , P_ASSET_TYPE           IN FI_ASSET_CATEGORY.ASSET_TYPE%TYPE
            , P_DPR_METHOD_TYPE      IN FI_ASSET_CATEGORY.DPR_METHOD_TYPE%TYPE
            , P_PROGRESS_YEAR        IN FI_ASSET_CATEGORY.PROGRESS_YEAR%TYPE
            , P_RESIDUAL_AMOUNT      IN FI_ASSET_CATEGORY.RESIDUAL_AMOUNT%TYPE
            , P_IFRS_DPR_METHOD_TYPE IN FI_ASSET_CATEGORY.IFRS_DPR_METHOD_TYPE%TYPE
            , P_IFRS_PROGRESS_YEAR   IN FI_ASSET_CATEGORY.IFRS_PROGRESS_YEAR%TYPE
            , P_IFRS_RESIDUAL_AMOUNT IN FI_ASSET_CATEGORY.IFRS_RESIDUAL_AMOUNT%TYPE
            , P_ACCOUNT_CONTROL_ID   IN FI_ASSET_CATEGORY.AST_ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE         IN FI_ASSET_CATEGORY.AST_ACCOUNT_CODE%TYPE
            , P_DPR_ACCOUNT_CONTROL_ID   IN FI_ASSET_CATEGORY.DPR_ACCOUNT_CONTROL_ID%TYPE
            , P_DPR_ACCOUNT_CODE         IN FI_ASSET_CATEGORY.DPR_ACCOUNT_CODE%TYPE
            , P_DPR_SUM_ACCOUNT_CONTROL_ID   IN FI_ASSET_CATEGORY.DPR_SUM_ACCOUNT_CONTROL_ID%TYPE
            , P_DPR_SUM_ACCOUNT_CODE         IN FI_ASSET_CATEGORY.DPR_SUM_ACCOUNT_CODE%TYPE
            , P_REMARK               IN FI_ASSET_CATEGORY.REMARK%TYPE
            , P_ENABLED_FLAG         IN FI_ASSET_CATEGORY.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR    IN FI_ASSET_CATEGORY.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO    IN FI_ASSET_CATEGORY.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID              IN FI_ASSET_CATEGORY.CREATED_BY%TYPE
            )
  AS
  BEGIN
    UPDATE FI_ASSET_CATEGORY
      SET ASSET_CATEGORY_NAME  = P_ASSET_CATEGORY_NAME
        , EXPENSE_TYPE         = P_EXPENSE_TYPE
        , ASSET_TYPE           = P_ASSET_TYPE
        , DPR_METHOD_TYPE      = P_DPR_METHOD_TYPE
        , PROGRESS_YEAR        = P_PROGRESS_YEAR
        , RESIDUAL_AMOUNT      = P_RESIDUAL_AMOUNT
        , IFRS_DPR_METHOD_TYPE = P_IFRS_DPR_METHOD_TYPE
        , IFRS_PROGRESS_YEAR   = P_IFRS_PROGRESS_YEAR
        , IFRS_RESIDUAL_AMOUNT = P_IFRS_RESIDUAL_AMOUNT
        , AST_ACCOUNT_CONTROL_ID   = P_ACCOUNT_CONTROL_ID
        , AST_ACCOUNT_CODE         = P_ACCOUNT_CODE
        , DPR_ACCOUNT_CONTROL_ID   = P_DPR_ACCOUNT_CONTROL_ID
        , DPR_ACCOUNT_CODE         = P_DPR_ACCOUNT_CODE
        , DPR_SUM_ACCOUNT_CONTROL_ID   = P_DPR_SUM_ACCOUNT_CONTROL_ID
        , DPR_SUM_ACCOUNT_CODE         = P_DPR_SUM_ACCOUNT_CODE        
        , REMARK               = P_REMARK
        , ENABLED_FLAG         = P_ENABLED_FLAG
        , EFFECTIVE_DATE_FR    = P_EFFECTIVE_DATE_FR
        , EFFECTIVE_DATE_TO    = P_EFFECTIVE_DATE_TO
        , LAST_UPDATE_DATE     =  GET_LOCAL_DATE(SOB_ID)
        , LAST_UPDATED_BY      = P_USER_ID
    WHERE ASSET_CATEGORY_ID    = W_ASSET_CATEGORY_ID
   ;

  END UPDATE_ASSET_CATEGORY;

-- 자산카테고리 LOOKUP 조회.
  PROCEDURE LU_ASSET_CATEGORY
            ( P_CURSOR3              OUT TYPES.TCURSOR3
            , W_SOB_ID               IN FI_BANK.SOB_ID%TYPE
            , W_ENABLED_YN           IN FI_BANK.ENABLED_FLAG%TYPE
            )
  AS
    V_STD_DATE                       DATE := NULL;

  BEGIN
    IF W_ENABLED_YN = 'Y' THEN
      V_STD_DATE := GET_LOCAL_DATE(W_SOB_ID);
    END IF;

    OPEN P_CURSOR3 FOR
      SELECT AC.ASSET_CATEGORY_NAME
           , AC.ASSET_CATEGORY_CODE
           , FI_COMMON_G.CODE_NAME_F('EXPENSE_TYPE', AC.EXPENSE_TYPE, AC.SOB_ID) AS EXPENSE_TYPE_NAME
           , AC.ASSET_CATEGORY_ID
        FROM FI_ASSET_CATEGORY AC
       WHERE AC.SOB_ID              = W_SOB_ID
         AND AC.ENABLED_FLAG        = DECODE(W_ENABLED_YN, 'Y', 'Y', AC.ENABLED_FLAG)
         AND AC.EFFECTIVE_DATE_FR   <= NVL(V_STD_DATE, AC.EFFECTIVE_DATE_FR)
         AND (AC.EFFECTIVE_DATE_TO IS NULL OR AC.EFFECTIVE_DATE_TO >= NVL(V_STD_DATE, AC.EFFECTIVE_DATE_TO))
      ORDER BY AC.ASSET_CATEGORY_CODE
      ;

  END LU_ASSET_CATEGORY;













--하기 부터가 임동언이 개발한 내용이다.(2011.10.18)





--조회
PROCEDURE LIST_ASSET_CATEGORY( 
      P_CURSOR  OUT TYPES.TCURSOR
    , W_SOB_ID  IN  FI_ASSET_CATEGORY.SOB_ID%TYPE
)

AS

BEGIN

    OPEN P_CURSOR FOR

    SELECT
          SOB_ID	            --회사아이디
        , ASSET_CATEGORY_ID	    --자산유형아이디
        , ASSET_CATEGORY_CODE	--자산유형코드
        , ASSET_CATEGORY_NAME	--자산유형명칭
        , (
            SELECT COUNT(*)
            FROM FI_ASSET_MASTER
            WHERE SOB_ID = W_SOB_ID
                AND ASSET_CATEGORY_ID = A.ASSET_CATEGORY_ID
          ) AS ASSET_CNT    --자산등록건수
        , ASSET_TYPE        --자산형태_코드(1:유형자산, 2:무형자산)
        , FI_COMMON_G.CODE_NAME_F('ASSET_TYPE', ASSET_TYPE, SOB_ID, NULL) AS ASSET_TYPE_NM  --자산형태_명
        , IFRS_DPR_METHOD_TYPE	--IFRS_감가상각방법_코드(1:정액,2:정율)
        , FI_COMMON_G.CODE_NAME_F('DPR_METHOD_TYPE', IFRS_DPR_METHOD_TYPE, SOB_ID, NULL) AS IFRS_DPR_METHOD_TYPE_NM --감가상각방법
        , IFRS_PROGRESS_YEAR	--IFRS_내용년수
        , IFRS_RESIDUAL_AMOUNT	--IFRS 잔존가치
        , REMARK                --비고
    FROM FI_ASSET_CATEGORY A
    WHERE SOB_ID = W_SOB_ID
    ORDER BY ASSET_CATEGORY_CODE    ;

END LIST_ASSET_CATEGORY;









--신규자료 등록
PROCEDURE INS_ASSET_CATEGORY( 
      P_SOB_ID                  IN  FI_ASSET_CATEGORY.SOB_ID%TYPE               --회사아이디
    , P_ASSET_CATEGORY_CODE     IN  FI_ASSET_CATEGORY.ASSET_CATEGORY_CODE%TYPE  --자산유형코드      
    , P_ASSET_CATEGORY_NAME     IN  FI_ASSET_CATEGORY.ASSET_CATEGORY_NAME%TYPE  --자산유형명칭
    , P_ASSET_TYPE              IN  FI_ASSET_CATEGORY.ASSET_TYPE%TYPE           --자산형태(1:유형자산, 2:무형자산)
    , P_IFRS_DPR_METHOD_TYPE    IN  FI_ASSET_CATEGORY.IFRS_DPR_METHOD_TYPE%TYPE --(IFRS)감가상각방법 1:정액,2:정율
    , P_IFRS_PROGRESS_YEAR      IN  FI_ASSET_CATEGORY.IFRS_PROGRESS_YEAR%TYPE   --(IFRS)내용년수
    , P_IFRS_RESIDUAL_AMOUNT    IN  FI_ASSET_CATEGORY.IFRS_RESIDUAL_AMOUNT%TYPE --(IFRS)잔존가치
    , P_REMARK                  IN  FI_ASSET_CATEGORY.REMARK%TYPE               --비고

    , P_CREATED_BY              IN FI_ASSET_CATEGORY.CREATED_BY%TYPE            --생성자
)

AS

V_SYSDATE   DATE := GET_LOCAL_DATE(P_SOB_ID);
t_CNT NUMBER := 0;

BEGIN

    --동일자료 존재 유무 파악(KEY 중복 여부 파악)    
    SELECT COUNT(*)
    INTO t_CNT
    FROM FI_ASSET_CATEGORY
    WHERE SOB_ID = P_SOB_ID
        AND ASSET_CATEGORY_CODE = P_ASSET_CATEGORY_CODE ;

    
    IF t_CNT > 0 THEN
      RAISE ERRNUMS.Exist_Data;
    END IF;


    INSERT INTO FI_ASSET_CATEGORY( 
          ASSET_CATEGORY_ID --자산구분아이디
        , ASSET_CATEGORY_CODE   --자산구분코드
        , ASSET_CATEGORY_NAME   --자산구분명칭
        , SOB_ID                --회사아이디
        , ASSET_TYPE            --자산형태(1:유형자산, 2:무형자산)
        , IFRS_DPR_METHOD_TYPE  --IFRS_감가상각방법 1:정액,2:정율
        , IFRS_PROGRESS_YEAR    --IFRS_내용년수
        , IFRS_RESIDUAL_AMOUNT  --IFRS_잔존가치
        , REMARK                --비고
        , CREATION_DATE         --생성일자
        , CREATED_BY            --생성자
        , LAST_UPDATE_DATE      --최종수정일자
        , LAST_UPDATED_BY       --최종수정자 
    )
    VALUES
    ( 
          (SELECT NVL(MAX(ASSET_CATEGORY_ID), 0) + 1 FROM FI_ASSET_CATEGORY)    --자산구분아이디
        , P_ASSET_CATEGORY_CODE     --자산구분코드
        , P_ASSET_CATEGORY_NAME     --자산구분명칭
        , P_SOB_ID                  --회사아이디
        , P_ASSET_TYPE              --자산형태(1:유형자산, 2:무형자산)
        , P_IFRS_DPR_METHOD_TYPE    --IFRS_감가상각방법 1:정액,2:정율
        , P_IFRS_PROGRESS_YEAR      --IFRS_내용년수
        , P_IFRS_RESIDUAL_AMOUNT    --IFRS_잔존가치
        , P_REMARK                  --비고
        , GET_LOCAL_DATE(P_SOB_ID)  --생성일자
        , P_CREATED_BY              --생성자
        , GET_LOCAL_DATE(P_SOB_ID)  --최종수정일자
        , P_CREATED_BY              --최종수정자 
    );

EXCEPTION
    WHEN ERRNUMS.Exist_Data THEN
        RAISE_APPLICATION_ERROR(ERRNUMS.Exist_Data_Code, ERRNUMS.Exist_Data_Desc);

END INS_ASSET_CATEGORY;










--수정
PROCEDURE UPD_ASSET_CATEGORY( 
      W_SOB_ID                  IN FI_ASSET_CATEGORY.SOB_ID%TYPE                --회사아이디
    , W_ASSET_CATEGORY_CODE     IN FI_ASSET_CATEGORY.ASSET_CATEGORY_CODE%TYPE   --자산구분코드
      
    , P_ASSET_CATEGORY_NAME     IN  FI_ASSET_CATEGORY.ASSET_CATEGORY_NAME%TYPE  --자산구분명칭
    , P_ASSET_TYPE              IN  FI_ASSET_CATEGORY.ASSET_TYPE%TYPE           --자산형태(1:유형자산, 2:무형자산)
    , P_IFRS_DPR_METHOD_TYPE    IN  FI_ASSET_CATEGORY.IFRS_DPR_METHOD_TYPE%TYPE --IFRS_감가상각방법 1:정액,2:정율
    , P_IFRS_PROGRESS_YEAR      IN  FI_ASSET_CATEGORY.IFRS_PROGRESS_YEAR%TYPE   --IFRS_내용년수
    , P_IFRS_RESIDUAL_AMOUNT    IN  FI_ASSET_CATEGORY.IFRS_RESIDUAL_AMOUNT%TYPE --IFRS_잔존가치
    , P_REMARK                  IN  FI_ASSET_CATEGORY.REMARK%TYPE               --비고    

    , P_LAST_UPDATED_BY         IN FI_ASSET_CATEGORY.LAST_UPDATED_BY%TYPE       --최종수정자
)

AS

BEGIN

    UPDATE FI_ASSET_CATEGORY
    SET
          ASSET_CATEGORY_NAME   = P_ASSET_CATEGORY_NAME     --자산구분명칭
        , ASSET_TYPE            = P_ASSET_TYPE              --자산형태       
        , IFRS_DPR_METHOD_TYPE  = P_IFRS_DPR_METHOD_TYPE    --IFRS_감가상각방법
        , IFRS_PROGRESS_YEAR    = P_IFRS_PROGRESS_YEAR      --IFRS_내용년수
        , IFRS_RESIDUAL_AMOUNT  = P_IFRS_RESIDUAL_AMOUNT    --IFRS_잔존가치     
        , REMARK                = P_REMARK                  --비고
        
        , LAST_UPDATE_DATE      = GET_LOCAL_DATE(SOB_ID)    --최종수정일자
        , LAST_UPDATED_BY       = P_LAST_UPDATED_BY         --최종수정자
    WHERE SOB_ID = W_SOB_ID
        AND ASSET_CATEGORY_CODE = W_ASSET_CATEGORY_CODE ;

END UPD_ASSET_CATEGORY;









--삭제
PROCEDURE DEL_ASSET_CATEGORY(
      W_SOB_ID                  IN FI_ASSET_CATEGORY.SOB_ID%TYPE                --회사아이디
    , W_ASSET_CATEGORY_CODE     IN FI_ASSET_CATEGORY.ASSET_CATEGORY_CODE%TYPE   --자산구분코드
)

AS

t_CNT   NUMBER := 0;
Key_Dup EXCEPTION;

BEGIN

    --해당 자산유형으로 등록된 자산이 있는지를 파악한다.        
    SELECT COUNT(*)
    INTO t_CNT
    FROM FI_ASSET_MASTER
    WHERE SOB_ID = W_SOB_ID
        AND ASSET_CATEGORY_ID =
                (
                    SELECT ASSET_CATEGORY_ID
                    FROM FI_ASSET_CATEGORY
                    WHERE SOB_ID = W_SOB_ID
                        AND ASSET_CATEGORY_CODE = W_ASSET_CATEGORY_CODE
                )   ;        
       
    IF t_CNT > 0 THEN
      RAISE Key_Dup;
    END IF;    


    --해당 자산유형으로 등록된 자산이 없으면 삭제한다.
    DELETE FI_ASSET_CATEGORY
    WHERE SOB_ID = W_SOB_ID
        AND ASSET_CATEGORY_CODE = W_ASSET_CATEGORY_CODE ;
        
        
EXCEPTION
    WHEN Key_Dup THEN
        --FCM_10307, 기준자료로 삭제할 수 없습니다.
        RAISE_APPLICATION_ERROR(-20001, EAPP_MESSAGE_G.RETURN_TEXT_F(USERENV_G.GET_TERRITORY_S_F, 'FCM_10307', NULL));

END DEL_ASSET_CATEGORY;









--자산유형 POPUP
PROCEDURE POPUP_ASSET_CATEGORY( 
      P_CURSOR  OUT TYPES.TCURSOR
    , W_SOB_ID  IN  FI_ASSET_CATEGORY.SOB_ID%TYPE
)

AS

BEGIN

    OPEN P_CURSOR FOR

    SELECT 
          ASSET_CATEGORY_NAME   --자산유형명칭
        , ASSET_CATEGORY_CODE   --자산유형코드
        , ASSET_CATEGORY_ID     --자산유형아이디
        , IFRS_DPR_METHOD_TYPE	--IFRS_감가상각방법_코드 (1:정액,2:정율)
        , FI_COMMON_G.CODE_NAME_F('DPR_METHOD_TYPE', IFRS_DPR_METHOD_TYPE, SOB_ID, NULL) AS IFRS_DPR_METHOD_TYPE_NM --감가상각방법    
        , IFRS_PROGRESS_YEAR	--IFRS_내용년수
        , IFRS_RESIDUAL_AMOUNT	--IFRS 잔존가치         
    FROM FI_ASSET_CATEGORY
    WHERE SOB_ID              = W_SOB_ID
    ORDER BY ASSET_CATEGORY_CODE    ;

END POPUP_ASSET_CATEGORY;








--자산유형명 반환
FUNCTION  F_ASSET_CATEGORY_NAME( 
    W_ASSET_CATEGORY_ID   IN   FI_ASSET_CATEGORY.ASSET_CATEGORY_ID%TYPE
) RETURN VARCHAR2

AS

t_ASSET_CATEGORY_NAME   FI_ASSET_CATEGORY.ASSET_CATEGORY_NAME%TYPE := '';

BEGIN

    SELECT ASSET_CATEGORY_NAME
    INTO t_ASSET_CATEGORY_NAME
    FROM FI_ASSET_CATEGORY
    WHERE ASSET_CATEGORY_ID = W_ASSET_CATEGORY_ID   ;

RETURN t_ASSET_CATEGORY_NAME;

END F_ASSET_CATEGORY_NAME   ;


END FI_ASSET_CATEGORY_G;
/
