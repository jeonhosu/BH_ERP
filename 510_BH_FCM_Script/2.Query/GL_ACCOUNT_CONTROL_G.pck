CREATE OR REPLACE PACKAGE GL_ACCOUNT_CONTROL_G
AS

-- 회계 계정통제관리 조회.
  PROCEDURE SELECT_ACCOUNT_CONTROL
            ( P_CURSOR               OUT TYPES.TCURSOR
            , W_ACCOUNT_SET_ID       IN GL_ACCOUNT_CONTROL.ACCOUNT_SET_ID%TYPE
            , W_ACCOUNT_GROUP_ID     IN GL_ACCOUNT_CONTROL.ACCOUNT_GROUP_ID%TYPE
            , W_SOB_ID               IN GL_ACCOUNT_CONTROL.SOB_ID%TYPE
            , W_ORG_ID               IN GL_ACCOUNT_CONTROL.ORG_ID%TYPE
            );

-- 회계 계정통제관리 삽입.
  PROCEDURE INSERT_ACCOUNT_CONTROL
            ( P_ACCOUNT_CONTROL_ID OUT GL_ACCOUNT_CONTROL.ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE       IN GL_ACCOUNT_CONTROL.ACCOUNT_CODE%TYPE
            , P_ACCOUNT_DESC       IN GL_ACCOUNT_CONTROL.ACCOUNT_DESC%TYPE
            , P_ACCOUNT_DESC_S     IN GL_ACCOUNT_CONTROL.ACCOUNT_DESC_S%TYPE
            , P_ACCOUNT_SET_ID     IN GL_ACCOUNT_CONTROL.ACCOUNT_SET_ID%TYPE
            , P_ACCOUNT_GROUP_ID   IN GL_ACCOUNT_CONTROL.ACCOUNT_GROUP_ID%TYPE
            , P_ACCOUNT_DR_CR      IN GL_ACCOUNT_CONTROL.ACCOUNT_DR_CR%TYPE
            , P_ACCOUNT_GL_ID      IN GL_ACCOUNT_CONTROL.ACCOUNT_GL_ID%TYPE
            , P_ACCOUNT_FS_TYPE    IN GL_ACCOUNT_CONTROL.ACCOUNT_FS_TYPE%TYPE
            , P_CUSTOMER_ENABLED_FLAG IN GL_ACCOUNT_CONTROL.CUSTOMER_ENABLED_FLAG%TYPE
            , P_ACCOUNT_ENABLED_FLAG  IN GL_ACCOUNT_CONTROL.ACCOUNT_ENABLED_FLAG%TYPE
            , P_BANK_ACCOUNT_FLAG     IN GL_ACCOUNT_CONTROL.BANK_ACCOUNT_FLAG%TYPE
            , P_CURRENCY_ENABLED_FLAG IN GL_ACCOUNT_CONTROL.CURRENCY_ENABLED_FLAG%TYPE
            , P_ACCOUNT_MICH_YN    IN GL_ACCOUNT_CONTROL.ACCOUNT_MICH_YN%TYPE
            , P_VAT_ENABLED_FLAG   IN GL_ACCOUNT_CONTROL.VAT_ENABLED_FLAG%TYPE 
            , P_BUDGET_ENABLED_FLAG   IN GL_ACCOUNT_CONTROL.BUDGET_ENABLED_FLAG%TYPE
            , P_BUDGET_CONTROL_FLAG    IN GL_ACCOUNT_CONTROL.BUDGET_CONTROL_FLAG%TYPE
            , P_BUDGET_BELONG_FLAG IN GL_ACCOUNT_CONTROL.BUDGET_BELONG_FLAG%TYPE
            , P_COST_CENTER_FLAG   IN GL_ACCOUNT_CONTROL.COST_CENTER_FLAG%TYPE
            , P_LIQUIDATE_METHOD_TYPE IN GL_ACCOUNT_CONTROL.LIQUIDATE_METHOD_TYPE%TYPE
            , P_ACCOUNT_CLASS_ID      IN GL_ACCOUNT_CONTROL.ACCOUNT_CLASS_ID%TYPE
            , P_REMARK             IN GL_ACCOUNT_CONTROL.REMARK%TYPE                       
            , P_ENABLED_FLAG       IN GL_ACCOUNT_CONTROL.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR  IN GL_ACCOUNT_CONTROL.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO  IN GL_ACCOUNT_CONTROL.EFFECTIVE_DATE_TO%TYPE
            , P_SOB_ID             IN GL_ACCOUNT_CONTROL.SOB_ID%TYPE
            , P_ORG_ID             IN GL_ACCOUNT_CONTROL.ORG_ID%TYPE
            , P_USER_ID            IN GL_ACCOUNT_CONTROL.CREATED_BY%TYPE
            );

-- 회계 계정통제관리 수정.
  PROCEDURE UPDATE_ACCOUNT_CONTROL
            ( W_ACCOUNT_CONTROL_ID IN GL_ACCOUNT_CONTROL.ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_DESC       IN GL_ACCOUNT_CONTROL.ACCOUNT_DESC%TYPE
            , P_ACCOUNT_DESC_S     IN GL_ACCOUNT_CONTROL.ACCOUNT_DESC_S%TYPE
            , P_ACCOUNT_DR_CR      IN GL_ACCOUNT_CONTROL.ACCOUNT_DR_CR%TYPE
            , P_ACCOUNT_GL_ID      IN GL_ACCOUNT_CONTROL.ACCOUNT_GL_ID%TYPE
            , P_ACCOUNT_FS_TYPE    IN GL_ACCOUNT_CONTROL.ACCOUNT_FS_TYPE%TYPE
            , P_CUSTOMER_ENABLED_FLAG IN GL_ACCOUNT_CONTROL.CUSTOMER_ENABLED_FLAG%TYPE
            , P_ACCOUNT_ENABLED_FLAG  IN GL_ACCOUNT_CONTROL.ACCOUNT_ENABLED_FLAG%TYPE
            , P_BANK_ACCOUNT_FLAG     IN GL_ACCOUNT_CONTROL.BANK_ACCOUNT_FLAG%TYPE
            , P_CURRENCY_ENABLED_FLAG IN GL_ACCOUNT_CONTROL.CURRENCY_ENABLED_FLAG%TYPE
            , P_ACCOUNT_MICH_YN    IN GL_ACCOUNT_CONTROL.ACCOUNT_MICH_YN%TYPE
            , P_VAT_ENABLED_FLAG   IN GL_ACCOUNT_CONTROL.VAT_ENABLED_FLAG%TYPE 
            , P_BUDGET_ENABLED_FLAG   IN GL_ACCOUNT_CONTROL.BUDGET_ENABLED_FLAG%TYPE
            , P_BUDGET_CONTROL_FLAG   IN GL_ACCOUNT_CONTROL.BUDGET_CONTROL_FLAG%TYPE
            , P_BUDGET_BELONG_FLAG IN GL_ACCOUNT_CONTROL.BUDGET_BELONG_FLAG%TYPE
            , P_COST_CENTER_FLAG   IN GL_ACCOUNT_CONTROL.COST_CENTER_FLAG%TYPE
            , P_LIQUIDATE_METHOD_TYPE IN GL_ACCOUNT_CONTROL.LIQUIDATE_METHOD_TYPE%TYPE
            , P_ACCOUNT_CLASS_ID      IN GL_ACCOUNT_CONTROL.ACCOUNT_CLASS_ID%TYPE
            , P_REMARK             IN GL_ACCOUNT_CONTROL.REMARK%TYPE                       
            , P_ENABLED_FLAG       IN GL_ACCOUNT_CONTROL.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR  IN GL_ACCOUNT_CONTROL.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO  IN GL_ACCOUNT_CONTROL.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID            IN GL_ACCOUNT_CONTROL.CREATED_BY%TYPE
            );


-- 회계계정통제 LOOKUP 조회.
  PROCEDURE LU_ACCOUNT_CONTROL
            ( P_CURSOR3              OUT TYPES.TCURSOR3
            , W_SOB_ID               IN GL_ACCOUNT_CONTROL.SOB_ID%TYPE
            , W_ENABLED_YN           IN GL_ACCOUNT_CONTROL.ENABLED_FLAG%TYPE
            );

---------------------------------------------------------------------------------------------------
-- 회계계정 코드.
  FUNCTION ACCOUNT_CODE_F
            ( W_ACCOUNT_CONTROL_ID   IN GL_ACCOUNT_CONTROL.ACCOUNT_CONTROL_ID%TYPE
            , W_SOB_ID               IN GL_ACCOUNT_CONTROL.SOB_ID%TYPE
            ) RETURN VARCHAR2;

-- 회계계정명.
  FUNCTION ACCOUNT_DESC_F
            ( W_ACCOUNT_CONTROL_ID   IN GL_ACCOUNT_CONTROL.ACCOUNT_CONTROL_ID%TYPE
            , W_SOB_ID               IN GL_ACCOUNT_CONTROL.SOB_ID%TYPE
            ) RETURN VARCHAR2;
            
-- 회계계정 분류 타입.
  PROCEDURE ACCOUNT_CLASS_TYPE_P
            ( W_ACCOUNT_BOOK_ID      IN FI_ACCOUNT_BOOK.ACCOUNT_BOOK_ID%TYPE
            , W_ACCOUNT_DR_CR        IN GL_ACCOUNT_CONTROL.ACCOUNT_DR_CR%TYPE
            , W_ACCOUNT_CONTROL_ID   IN GL_ACCOUNT_CONTROL.ACCOUNT_CONTROL_ID%TYPE
            , W_SOB_ID               IN GL_ACCOUNT_CONTROL.SOB_ID%TYPE
            , W_ORG_ID               IN GL_ACCOUNT_CONTROL.ORG_ID%TYPE
            , O_ACCOUNT_CLASS_ACTION OUT VARCHAR2
            , O_ACCOUNT_CLASS_TYPE   OUT VARCHAR2
            , O_BTN_CAPTION          OUT VARCHAR2
            );
            
END GL_ACCOUNT_CONTROL_G;
/
CREATE OR REPLACE PACKAGE BODY GL_ACCOUNT_CONTROL_G
AS
/******************************************************************************/
/* Project      : FPCB ERP
/* Module       : HR
/* Program Name : GL_ACCOUNT_CONTROL_G
/* Description  : 회계 계정통제관리 관리.
/*
/* Reference by :
/* Program History :
/*------------------------------------------------------------------------------
/*   Date       In Charge          Description
/*------------------------------------------------------------------------------
/* 07-JUN-2010  Jeon Ho Su          Initialize
/******************************************************************************/
-- 회계 계정통제관리 조회.
  PROCEDURE SELECT_ACCOUNT_CONTROL
            ( P_CURSOR               OUT TYPES.TCURSOR
            , W_ACCOUNT_SET_ID       IN GL_ACCOUNT_CONTROL.ACCOUNT_SET_ID%TYPE
            , W_ACCOUNT_GROUP_ID     IN GL_ACCOUNT_CONTROL.ACCOUNT_GROUP_ID%TYPE
            , W_SOB_ID               IN GL_ACCOUNT_CONTROL.SOB_ID%TYPE
            , W_ORG_ID               IN GL_ACCOUNT_CONTROL.ORG_ID%TYPE
            )
  AS
  BEGIN
    OPEN P_CURSOR FOR
      SELECT AC.ACCOUNT_CONTROL_ID
           , AC.ACCOUNT_CODE
           , AC.ACCOUNT_DESC
           , AC.ACCOUNT_DESC_S
           , AC.ACCOUNT_SET_ID
           , AC.ACCOUNT_GROUP_ID
           , AC.ACCOUNT_DR_CR
           , FI_COMMON_G.CODE_NAME_F('ACCOUNT_DR_CR', AC.ACCOUNT_DR_CR, AC.SOB_ID) AS ACCOUNT_DR_CR_NAME
           , AC.ACCOUNT_GL_ID
           , FI_COMMON_G.ID_NAME_F(AC.ACCOUNT_GL_ID) AS ACCOUNT_GL_NAME
           , AC.ACCOUNT_FS_TYPE
           , FI_COMMON_G.CODE_NAME_F('FS_TYPE', AC.ACCOUNT_FS_TYPE, AC.SOB_ID) AS ACCOUNT_FS_TYPE_NAME
           , AC.CUSTOMER_ENABLED_FLAG
           , AC.ACCOUNT_ENABLED_FLAG
           , AC.BANK_ACCOUNT_FLAG
           , AC.CURRENCY_ENABLED_FLAG
           , AC.ACCOUNT_MICH_YN
           , AC.VAT_ENABLED_FLAG
           , AC.BUDGET_ENABLED_FLAG
           , AC.BUDGET_CONTROL_FLAG
           , AC.BUDGET_BELONG_FLAG
           , AC.COST_CENTER_FLAG
           , AC.LIQUIDATE_METHOD_TYPE
           , FI_COMMON_G.CODE_NAME_F('LIQUIDATE_METHOD_TYPE', AC.LIQUIDATE_METHOD_TYPE, AC.SOB_ID) AS LIQUIDATE_METHOD_TYPE_NAME
           , AC.ACCOUNT_CLASS_ID
           , FI_COMMON_G.ID_NAME_F(AC.ACCOUNT_CLASS_ID) AS ACCOUNT_CLASS_NAME
           , AC.REMARK
           , AC.ENABLED_FLAG 
           , AC.EFFECTIVE_DATE_FR
           , AC.EFFECTIVE_DATE_TO
        FROM GL_ACCOUNT_CONTROL AC
       WHERE AC.ACCOUNT_SET_ID          = W_ACCOUNT_SET_ID
         AND AC.ACCOUNT_GROUP_ID        = W_ACCOUNT_GROUP_ID
         AND AC.SOB_ID                  = W_SOB_ID
/*         AND AC.ORG_ID                  = W_ORG_ID*/
      ;

  END SELECT_ACCOUNT_CONTROL;

-- 회계 계정통제관리 삽입.
  PROCEDURE INSERT_ACCOUNT_CONTROL
            ( P_ACCOUNT_CONTROL_ID OUT GL_ACCOUNT_CONTROL.ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE       IN GL_ACCOUNT_CONTROL.ACCOUNT_CODE%TYPE
            , P_ACCOUNT_DESC       IN GL_ACCOUNT_CONTROL.ACCOUNT_DESC%TYPE
            , P_ACCOUNT_DESC_S     IN GL_ACCOUNT_CONTROL.ACCOUNT_DESC_S%TYPE
            , P_ACCOUNT_SET_ID     IN GL_ACCOUNT_CONTROL.ACCOUNT_SET_ID%TYPE
            , P_ACCOUNT_GROUP_ID   IN GL_ACCOUNT_CONTROL.ACCOUNT_GROUP_ID%TYPE
            , P_ACCOUNT_DR_CR      IN GL_ACCOUNT_CONTROL.ACCOUNT_DR_CR%TYPE
            , P_ACCOUNT_GL_ID      IN GL_ACCOUNT_CONTROL.ACCOUNT_GL_ID%TYPE
            , P_ACCOUNT_FS_TYPE    IN GL_ACCOUNT_CONTROL.ACCOUNT_FS_TYPE%TYPE
            , P_CUSTOMER_ENABLED_FLAG IN GL_ACCOUNT_CONTROL.CUSTOMER_ENABLED_FLAG%TYPE
            , P_ACCOUNT_ENABLED_FLAG  IN GL_ACCOUNT_CONTROL.ACCOUNT_ENABLED_FLAG%TYPE
            , P_BANK_ACCOUNT_FLAG     IN GL_ACCOUNT_CONTROL.BANK_ACCOUNT_FLAG%TYPE
            , P_CURRENCY_ENABLED_FLAG IN GL_ACCOUNT_CONTROL.CURRENCY_ENABLED_FLAG%TYPE
            , P_ACCOUNT_MICH_YN    IN GL_ACCOUNT_CONTROL.ACCOUNT_MICH_YN%TYPE
            , P_VAT_ENABLED_FLAG   IN GL_ACCOUNT_CONTROL.VAT_ENABLED_FLAG%TYPE 
            , P_BUDGET_ENABLED_FLAG   IN GL_ACCOUNT_CONTROL.BUDGET_ENABLED_FLAG%TYPE
            , P_BUDGET_CONTROL_FLAG   IN GL_ACCOUNT_CONTROL.BUDGET_CONTROL_FLAG%TYPE
            , P_BUDGET_BELONG_FLAG IN GL_ACCOUNT_CONTROL.BUDGET_BELONG_FLAG%TYPE
            , P_COST_CENTER_FLAG   IN GL_ACCOUNT_CONTROL.COST_CENTER_FLAG%TYPE
            , P_LIQUIDATE_METHOD_TYPE IN GL_ACCOUNT_CONTROL.LIQUIDATE_METHOD_TYPE%TYPE
            , P_ACCOUNT_CLASS_ID      IN GL_ACCOUNT_CONTROL.ACCOUNT_CLASS_ID%TYPE
            , P_REMARK             IN GL_ACCOUNT_CONTROL.REMARK%TYPE                       
            , P_ENABLED_FLAG       IN GL_ACCOUNT_CONTROL.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR  IN GL_ACCOUNT_CONTROL.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO  IN GL_ACCOUNT_CONTROL.EFFECTIVE_DATE_TO%TYPE
            , P_SOB_ID             IN GL_ACCOUNT_CONTROL.SOB_ID%TYPE
            , P_ORG_ID             IN GL_ACCOUNT_CONTROL.ORG_ID%TYPE
            , P_USER_ID            IN GL_ACCOUNT_CONTROL.CREATED_BY%TYPE
            )
  AS
    V_SYSDATE                      GL_ACCOUNT_CONTROL.CREATION_DATE%TYPE;
    V_RECORD_COUNT                 NUMBER := 0;

  BEGIN
    V_SYSDATE := GET_LOCAL_DATE(P_SOB_ID);
    V_RECORD_COUNT := 0;
    BEGIN
      SELECT COUNT(GAC.ACCOUNT_CODE) AS RECORD_COUNT
        INTO V_RECORD_COUNT
        FROM GL_ACCOUNT_CONTROL GAC
       WHERE GAC.ACCOUNT_CODE     = P_ACCOUNT_CODE
         AND GAC.ACCOUNT_SET_ID   = P_ACCOUNT_SET_ID
         AND GAC.SOB_ID           = P_SOB_ID
      ;
    EXCEPTION WHEN OTHERS THEN
      V_RECORD_COUNT := 0;
    END;
    IF V_RECORD_COUNT > 0 THEN
     RAISE ERRNUMS.Exist_Data;
    END IF;
-------------------------------------------------
    SELECT GL_ACCOUNT_CONTROL_S1.NEXTVAL
     INTO P_ACCOUNT_CONTROL_ID
     FROM DUAL;

    INSERT INTO GL_ACCOUNT_CONTROL
    ( ACCOUNT_CONTROL_ID
    , ACCOUNT_CODE 
    , ACCOUNT_DESC 
    , ACCOUNT_DESC_S 
    , ACCOUNT_SET_ID 
    , ACCOUNT_GROUP_ID 
    , ACCOUNT_DR_CR
    , ACCOUNT_GL_ID       
    , ACCOUNT_FS_TYPE
    , CUSTOMER_ENABLED_FLAG 
    , ACCOUNT_ENABLED_FLAG  
    , BANK_ACCOUNT_FLAG
    , CURRENCY_ENABLED_FLAG   
    , ACCOUNT_MICH_YN 
    , VAT_ENABLED_FLAG      
    , BUDGET_ENABLED_FLAG   
    , BUDGET_CONTROL_FLAG   
    , BUDGET_BELONG_FLAG    
    , COST_CENTER_FLAG 
    , LIQUIDATE_METHOD_TYPE
    , ACCOUNT_CLASS_ID
    , REMARK                
    , ENABLED_FLAG
    , EFFECTIVE_DATE_FR
    , EFFECTIVE_DATE_TO
    , SOB_ID 
    , ORG_ID
    , CREATION_DATE 
    , CREATED_BY 
    , LAST_UPDATE_DATE 
    , LAST_UPDATED_BY )
    VALUES
    ( P_ACCOUNT_CONTROL_ID
    , P_ACCOUNT_CODE
    , P_ACCOUNT_DESC
    , P_ACCOUNT_DESC_S
    , P_ACCOUNT_SET_ID
    , P_ACCOUNT_GROUP_ID
    , P_ACCOUNT_DR_CR
    , P_ACCOUNT_GL_ID
    , P_ACCOUNT_FS_TYPE
    , P_CUSTOMER_ENABLED_FLAG
    , P_ACCOUNT_ENABLED_FLAG 
    , P_BANK_ACCOUNT_FLAG
    , P_CURRENCY_ENABLED_FLAG
    , P_ACCOUNT_MICH_YN
    , P_VAT_ENABLED_FLAG    
    , P_BUDGET_ENABLED_FLAG
    , P_BUDGET_CONTROL_FLAG
    , P_BUDGET_BELONG_FLAG 
    , P_COST_CENTER_FLAG
    , P_LIQUIDATE_METHOD_TYPE
    , P_ACCOUNT_CLASS_ID
    , P_REMARK
    , P_ENABLED_FLAG
    , P_EFFECTIVE_DATE_FR
    , P_EFFECTIVE_DATE_TO
    , P_SOB_ID
    , P_ORG_ID
    , V_SYSDATE
    , P_USER_ID
    , V_SYSDATE
    , P_USER_ID );

  EXCEPTION
    WHEN ERRNUMS.Exist_Data THEN
    RAISE_APPLICATION_ERROR(ERRNUMS.Exist_Data_Code, ERRNUMS.Exist_Data_Desc);
  END INSERT_ACCOUNT_CONTROL;

-- 회계 계정통제관리 수정.
  PROCEDURE UPDATE_ACCOUNT_CONTROL
            ( W_ACCOUNT_CONTROL_ID IN GL_ACCOUNT_CONTROL.ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_DESC       IN GL_ACCOUNT_CONTROL.ACCOUNT_DESC%TYPE
            , P_ACCOUNT_DESC_S     IN GL_ACCOUNT_CONTROL.ACCOUNT_DESC_S%TYPE
            , P_ACCOUNT_DR_CR      IN GL_ACCOUNT_CONTROL.ACCOUNT_DR_CR%TYPE
            , P_ACCOUNT_GL_ID      IN GL_ACCOUNT_CONTROL.ACCOUNT_GL_ID%TYPE
            , P_ACCOUNT_FS_TYPE    IN GL_ACCOUNT_CONTROL.ACCOUNT_FS_TYPE%TYPE
            , P_CUSTOMER_ENABLED_FLAG IN GL_ACCOUNT_CONTROL.CUSTOMER_ENABLED_FLAG%TYPE
            , P_ACCOUNT_ENABLED_FLAG  IN GL_ACCOUNT_CONTROL.ACCOUNT_ENABLED_FLAG%TYPE
            , P_BANK_ACCOUNT_FLAG     IN GL_ACCOUNT_CONTROL.BANK_ACCOUNT_FLAG%TYPE
            , P_CURRENCY_ENABLED_FLAG IN GL_ACCOUNT_CONTROL.CURRENCY_ENABLED_FLAG%TYPE
            , P_ACCOUNT_MICH_YN    IN GL_ACCOUNT_CONTROL.ACCOUNT_MICH_YN%TYPE
            , P_VAT_ENABLED_FLAG   IN GL_ACCOUNT_CONTROL.VAT_ENABLED_FLAG%TYPE 
            , P_BUDGET_ENABLED_FLAG   IN GL_ACCOUNT_CONTROL.BUDGET_ENABLED_FLAG%TYPE
            , P_BUDGET_CONTROL_FLAG   IN GL_ACCOUNT_CONTROL.BUDGET_CONTROL_FLAG%TYPE
            , P_BUDGET_BELONG_FLAG IN GL_ACCOUNT_CONTROL.BUDGET_BELONG_FLAG%TYPE
            , P_COST_CENTER_FLAG   IN GL_ACCOUNT_CONTROL.COST_CENTER_FLAG%TYPE
            , P_LIQUIDATE_METHOD_TYPE IN GL_ACCOUNT_CONTROL.LIQUIDATE_METHOD_TYPE%TYPE
            , P_ACCOUNT_CLASS_ID      IN GL_ACCOUNT_CONTROL.ACCOUNT_CLASS_ID%TYPE
            , P_REMARK             IN GL_ACCOUNT_CONTROL.REMARK%TYPE                       
            , P_ENABLED_FLAG       IN GL_ACCOUNT_CONTROL.ENABLED_FLAG%TYPE
            , P_EFFECTIVE_DATE_FR  IN GL_ACCOUNT_CONTROL.EFFECTIVE_DATE_FR%TYPE
            , P_EFFECTIVE_DATE_TO  IN GL_ACCOUNT_CONTROL.EFFECTIVE_DATE_TO%TYPE
            , P_USER_ID            IN GL_ACCOUNT_CONTROL.CREATED_BY%TYPE
            )
  AS
  BEGIN
    UPDATE GL_ACCOUNT_CONTROL
      SET ACCOUNT_DESC       = P_ACCOUNT_DESC
        , ACCOUNT_DESC_S     = P_ACCOUNT_DESC_S
        , ACCOUNT_DR_CR      = P_ACCOUNT_DR_CR
        , ACCOUNT_GL_ID      = P_ACCOUNT_GL_ID
        , ACCOUNT_FS_TYPE    = P_ACCOUNT_FS_TYPE
        , CUSTOMER_ENABLED_FLAG = P_CUSTOMER_ENABLED_FLAG 
        , ACCOUNT_ENABLED_FLAG  = P_ACCOUNT_ENABLED_FLAG
        , BANK_ACCOUNT_FLAG     = P_BANK_ACCOUNT_FLAG
        , CURRENCY_ENABLED_FLAG = P_CURRENCY_ENABLED_FLAG
        , ACCOUNT_MICH_YN    = P_ACCOUNT_MICH_YN
        , VAT_ENABLED_FLAG   = P_VAT_ENABLED_FLAG
        , BUDGET_ENABLED_FLAG   = P_BUDGET_ENABLED_FLAG
        , BUDGET_CONTROL_FLAG   = P_BUDGET_CONTROL_FLAG
        , BUDGET_BELONG_FLAG    = P_BUDGET_BELONG_FLAG
        , COST_CENTER_FLAG   = P_COST_CENTER_FLAG
        , LIQUIDATE_METHOD_TYPE = P_LIQUIDATE_METHOD_TYPE
        , ACCOUNT_CLASS_ID      = P_ACCOUNT_CLASS_ID
        , REMARK             = P_REMARK
        , ENABLED_FLAG       = P_ENABLED_FLAG
        , EFFECTIVE_DATE_FR  = P_EFFECTIVE_DATE_FR
        , EFFECTIVE_DATE_TO  = P_EFFECTIVE_DATE_TO
        , LAST_UPDATE_DATE   = GET_LOCAL_DATE(SOB_ID)
        , LAST_UPDATED_BY    = P_USER_ID
    WHERE ACCOUNT_CONTROL_ID = W_ACCOUNT_CONTROL_ID
    ;
    
  END UPDATE_ACCOUNT_CONTROL;

-- 회계계정통제 LOOKUP 조회.
  PROCEDURE LU_ACCOUNT_CONTROL
            ( P_CURSOR3              OUT TYPES.TCURSOR3
            , W_SOB_ID               IN GL_ACCOUNT_CONTROL.SOB_ID%TYPE
            , W_ENABLED_YN           IN GL_ACCOUNT_CONTROL.ENABLED_FLAG%TYPE
            )
  AS
    V_STD_DATE                       DATE := NULL;

  BEGIN
    IF W_ENABLED_YN = 'Y' THEN
      V_STD_DATE := GET_LOCAL_DATE(W_SOB_ID);
    END IF;
    
    OPEN P_CURSOR3 FOR
      SELECT AC.ACCOUNT_DESC
           , AC.ACCOUNT_CODE
           , AC.ACCOUNT_CONTROL_ID
           , AC.ACCOUNT_DR_CR
           , AC.CUSTOMER_ENABLED_FLAG
           , AC.ACCOUNT_ENABLED_FLAG
           , AC.BANK_ACCOUNT_FLAG
           , AC.CURRENCY_ENABLED_FLAG
           , AC.VAT_ENABLED_FLAG
           , AC.ACCOUNT_MICH_YN
           , AC.BUDGET_ENABLED_FLAG
           , AC.BUDGET_CONTROL_FLAG
           , AC.BUDGET_BELONG_FLAG
           , AC.COST_CENTER_FLAG
        FROM GL_ACCOUNT_CONTROL AC
       WHERE AC.ACCOUNT_SET_ID          = FI_ACCOUNT_BOOK_G.OPERATING_ACCOUNT_SET_F(W_SOB_ID)
         AND AC.SOB_ID                  = W_SOB_ID
         AND AC.ENABLED_FLAG            = DECODE(W_ENABLED_YN, 'Y', 'Y', AC.ENABLED_FLAG)
         AND AC.EFFECTIVE_DATE_FR       <= NVL(V_STD_DATE, AC.EFFECTIVE_DATE_FR)
         AND (AC.EFFECTIVE_DATE_TO IS NULL OR AC.EFFECTIVE_DATE_TO >= NVL(V_STD_DATE, AC.EFFECTIVE_DATE_TO))
      ORDER BY AC.ACCOUNT_CODE         
      ;
  
  END LU_ACCOUNT_CONTROL;
  
---------------------------------------------------------------------------------------------------
-- 회계계정 코드.
  FUNCTION ACCOUNT_CODE_F
            ( W_ACCOUNT_CONTROL_ID   IN GL_ACCOUNT_CONTROL.ACCOUNT_CONTROL_ID%TYPE
            , W_SOB_ID               IN GL_ACCOUNT_CONTROL.SOB_ID%TYPE
            ) RETURN VARCHAR2
  AS
    V_ACCOUNT_CODE                   GL_ACCOUNT_CONTROL.ACCOUNT_CODE%TYPE := NULL;
  BEGIN
    BEGIN
      SELECT AC.ACCOUNT_CODE
        INTO V_ACCOUNT_CODE
        FROM GL_ACCOUNT_CONTROL AC
       WHERE AC.ACCOUNT_CONTROL_ID      = W_ACCOUNT_CONTROL_ID
         AND AC.SOB_ID                  = W_SOB_ID
      ;
    EXCEPTION WHEN OTHERS THEN
      V_ACCOUNT_CODE := NULL;
    END;
    RETURN V_ACCOUNT_CODE;
    
  END ACCOUNT_CODE_F;

-- 회계계정명.
  FUNCTION ACCOUNT_DESC_F
            ( W_ACCOUNT_CONTROL_ID   IN GL_ACCOUNT_CONTROL.ACCOUNT_CONTROL_ID%TYPE
            , W_SOB_ID               IN GL_ACCOUNT_CONTROL.SOB_ID%TYPE
            ) RETURN VARCHAR2
  AS
    V_ACCOUNT_DESC                   GL_ACCOUNT_CONTROL.ACCOUNT_DESC%TYPE := NULL;
  BEGIN
    BEGIN
      SELECT AC.ACCOUNT_DESC
        INTO V_ACCOUNT_DESC
        FROM GL_ACCOUNT_CONTROL AC
       WHERE AC.ACCOUNT_CONTROL_ID      = W_ACCOUNT_CONTROL_ID
         AND AC.SOB_ID                  = W_SOB_ID
      ;
    EXCEPTION WHEN OTHERS THEN
      V_ACCOUNT_DESC := NULL;
    END;
    RETURN V_ACCOUNT_DESC;
    
  END ACCOUNT_DESC_F;

-- 회계계정 분류 타입.
  PROCEDURE ACCOUNT_CLASS_TYPE_P
            ( W_ACCOUNT_BOOK_ID      IN FI_ACCOUNT_BOOK.ACCOUNT_BOOK_ID%TYPE
            , W_ACCOUNT_DR_CR        IN GL_ACCOUNT_CONTROL.ACCOUNT_DR_CR%TYPE
            , W_ACCOUNT_CONTROL_ID   IN GL_ACCOUNT_CONTROL.ACCOUNT_CONTROL_ID%TYPE
            , W_SOB_ID               IN GL_ACCOUNT_CONTROL.SOB_ID%TYPE
            , W_ORG_ID               IN GL_ACCOUNT_CONTROL.ORG_ID%TYPE
            , O_ACCOUNT_CLASS_ACTION OUT VARCHAR2
            , O_ACCOUNT_CLASS_TYPE   OUT VARCHAR2
            , O_BTN_CAPTION          OUT VARCHAR2
            )
  AS
    V_ACCOUNT_BOOK_ID                FI_ACCOUNT_BOOK.ACCOUNT_BOOK_ID%TYPE;
    V_ACCOUNT_DR_CR                  GL_ACCOUNT_CONTROL.ACCOUNT_DR_CR%TYPE;
    V_UNLIQUIDATE_YN                 VARCHAR2(1);
    V_ACCOUNT_CLASS_ACTION           VARCHAR2(100);
    V_ACCOUNT_CLASS_TYPE             VARCHAR2(100);
    
  BEGIN
    IF W_ACCOUNT_BOOK_ID IS NULL THEN
      V_ACCOUNT_BOOK_ID := FI_ACCOUNT_BOOK_G.OPERATING_ACCOUNT_SET_F(W_SOB_ID);
    ELSE
      V_ACCOUNT_BOOK_ID := W_ACCOUNT_BOOK_ID;
    END IF;
    
    BEGIN
      SELECT AC.ACCOUNT_DR_CR
           , AC.ACCOUNT_MICH_YN
           , ACL.ACCOUNT_CLASS_TYPE
        INTO V_ACCOUNT_DR_CR
           , V_UNLIQUIDATE_YN
           , V_ACCOUNT_CLASS_TYPE
        FROM GL_ACCOUNT_CONTROL AC
          , FI_ACCOUNT_CLASS_V ACL
       WHERE AC.ACCOUNT_CLASS_ID        = ACL.ACCOUNT_CLASS_ID(+)
         AND AC.ACCOUNT_SET_ID          = V_ACCOUNT_BOOK_ID
         AND AC.ACCOUNT_CONTROL_ID      = W_ACCOUNT_CONTROL_ID
         AND AC.SOB_ID                  = W_SOB_ID 
      ;
    EXCEPTION WHEN OTHERS THEN
      V_ACCOUNT_DR_CR := NULL;
      V_UNLIQUIDATE_YN := NULL;
      V_ACCOUNT_CLASS_TYPE := NULL;
    END;
    -- ACTION 설정.
    O_ACCOUNT_CLASS_ACTION := NULL;
    O_ACCOUNT_CLASS_TYPE := NULL;  
    O_BTN_CAPTION := NULL;        
    IF V_ACCOUNT_DR_CR IS NULL THEN
      O_ACCOUNT_CLASS_ACTION := 'NON';
    ELSIF W_ACCOUNT_DR_CR = V_ACCOUNT_DR_CR THEN
      O_ACCOUNT_CLASS_ACTION := 'NEW';      
    ELSE
      O_ACCOUNT_CLASS_ACTION := 'CLOSE';    
    END IF;
    
    -- 계정 분류.
    O_BTN_CAPTION := NULL;
    IF V_UNLIQUIDATE_YN = 'Y' AND O_ACCOUNT_CLASS_ACTION = 'CLOSE' THEN
    -- 반제 처리(잔액 상대편에 위치).
      O_ACCOUNT_CLASS_TYPE := 'UNLIQUIDATE';
      O_BTN_CAPTION := EAPP_MESSAGE_G.RETURN_TEXT_F(USERENV_G.GET_TERRITORY_S_F, 'FCM_10138', NULL);
    ELSIF V_ACCOUNT_CLASS_TYPE = 'RECEIVABLE_NOTE' THEN
    -- 받을 어음/차대 구분없이 적용.
      O_ACCOUNT_CLASS_TYPE := 'RECEIVABLE_NOTE';
      O_BTN_CAPTION := EAPP_MESSAGE_G.RETURN_TEXT_F(USERENV_G.GET_TERRITORY_S_F, 'FCM_10139', NULL);
    ELSIF V_ACCOUNT_CLASS_TYPE = 'PAYABLE_NOTE' AND O_ACCOUNT_CLASS_ACTION = 'NEW' THEN
    -- 지급어음/지급일 경우 적용.
      O_ACCOUNT_CLASS_TYPE := 'PAYABLE_NOTE';
      O_BTN_CAPTION := EAPP_MESSAGE_G.RETURN_TEXT_F(USERENV_G.GET_TERRITORY_S_F, 'FCM_10140', NULL);
    END IF;
    
  END ACCOUNT_CLASS_TYPE_P;
  
END GL_ACCOUNT_CONTROL_G;
/
