CREATE OR REPLACE PACKAGE FI_CLOSING_AUTO_JOURNAL_G
AS

-- 결산계정 조회.
  PROCEDURE SELECT_AUTO_JOURNAL
            ( P_CURSOR              OUT TYPES.TCURSOR
            , P_CLOSING_GROUP       IN FI_CLOSING_AUTO_JOURNAL.CLOSING_GROUP%TYPE
            , P_ACCOUNT_CONTROL_ID  IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_CONTROL_ID%TYPE
            , P_SOB_ID              IN FI_CLOSING_AUTO_JOURNAL.SOB_ID%TYPE
            );

-- 결산자동분개 삽입
  PROCEDURE INSERT_AUTO_JOURNAL
            ( P_JOURNAL_ID         OUT FI_CLOSING_AUTO_JOURNAL.JOURNAL_ID%TYPE
            , P_SOB_ID             IN FI_CLOSING_AUTO_JOURNAL.SOB_ID%TYPE
            , P_ORG_ID             IN FI_CLOSING_AUTO_JOURNAL.ORG_ID%TYPE
            , P_CLOSING_GROUP      IN FI_CLOSING_AUTO_JOURNAL.CLOSING_GROUP%TYPE
            , P_ACCOUNT_CONTROL_ID IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE       IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_CODE%TYPE
            , P_ACCOUNT_DR_CR      IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_DR_CR%TYPE
            , P_CONTRA_ACCOUNT_FLAG IN FI_CLOSING_AUTO_JOURNAL.CONTRA_ACCOUNT_FLAG%TYPE
            , P_REMARK             IN FI_CLOSING_AUTO_JOURNAL.REMARK%TYPE
            , P_USER_ID            IN FI_CLOSING_AUTO_JOURNAL.CREATED_BY%TYPE
            );

-- 결산자동분개 수정.
  PROCEDURE UPDATE_AUTO_JOURNAL
            ( W_JOURNAL_ID         IN FI_CLOSING_AUTO_JOURNAL.JOURNAL_ID%TYPE
            , W_SOB_ID             IN FI_CLOSING_AUTO_JOURNAL.SOB_ID%TYPE
            , P_CLOSING_GROUP      IN FI_CLOSING_AUTO_JOURNAL.CLOSING_GROUP%TYPE
            , P_ACCOUNT_CONTROL_ID IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE       IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_CODE%TYPE
            , P_ACCOUNT_DR_CR      IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_DR_CR%TYPE
            , P_CONTRA_ACCOUNT_FLAG IN FI_CLOSING_AUTO_JOURNAL.CONTRA_ACCOUNT_FLAG%TYPE
            , P_REMARK             IN FI_CLOSING_AUTO_JOURNAL.REMARK%TYPE
            , P_USER_ID            IN FI_CLOSING_AUTO_JOURNAL.CREATED_BY%TYPE
            );

-- 결산자동분개 삭제.
  PROCEDURE DELETE_AUTO_JOURNAL
            ( W_JOURNAL_ID         IN FI_CLOSING_AUTO_JOURNAL.JOURNAL_ID%TYPE
            );

END FI_CLOSING_AUTO_JOURNAL_G;
/
CREATE OR REPLACE PACKAGE BODY FI_CLOSING_AUTO_JOURNAL_G
AS
/******************************************************************************/
/* Project      : FPCB ERP
/* Module       : FCM
/* Program Name : FI_CLOSING_AUTO_JOURNAL_G
/* Description  : 결산계정 자동분개 관리
/*
/* Reference by :
/* Program History :
/*------------------------------------------------------------------------------
/*   Date       In Charge          Description
/*------------------------------------------------------------------------------
/* 07-JUN-2010  Jeon Ho Su          Initialize
/******************************************************************************/
-- 결산계정 조회.
  PROCEDURE SELECT_AUTO_JOURNAL
            ( P_CURSOR              OUT TYPES.TCURSOR
            , P_CLOSING_GROUP       IN FI_CLOSING_AUTO_JOURNAL.CLOSING_GROUP%TYPE
            , P_ACCOUNT_CONTROL_ID  IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_CONTROL_ID%TYPE
            , P_SOB_ID              IN FI_CLOSING_AUTO_JOURNAL.SOB_ID%TYPE
            )
  AS
  BEGIN
    OPEN P_CURSOR FOR
      SELECT CAJ.JOURNAL_ID
           , FI_COMMON_G.CODE_NAME_F('CLOSING_GROUP', CAJ.CLOSING_GROUP, CAJ.SOB_ID) AS CLOSING_GROUP_NAME
           , CAJ.CLOSING_GROUP
           , CAJ.ACCOUNT_CONTROL_ID
           , CAJ.ACCOUNT_CODE
           , AC.ACCOUNT_DESC
           , FI_COMMON_G.CODE_NAME_F('ACCOUNT_DR_CR', CAJ.ACCOUNT_DR_CR, CAJ.SOB_ID) AS ACCOUNT_DR_CR_NAME
           , CAJ.ACCOUNT_DR_CR
           , CAJ.CONTRA_ACCOUNT_FLAG
           , CAJ.REMARK
        FROM FI_CLOSING_AUTO_JOURNAL CAJ
          , FI_ACCOUNT_CONTROL AC
      WHERE CAJ.ACCOUNT_CONTROL_ID      = AC.ACCOUNT_CONTROL_ID
        AND CAJ.CLOSING_GROUP           = NVL(P_CLOSING_GROUP, CAJ.CLOSING_GROUP)
        AND CAJ.ACCOUNT_CONTROL_ID      = NVL(P_ACCOUNT_CONTROL_ID, CAJ.ACCOUNT_CONTROL_ID)
        AND CAJ.SOB_ID                  = P_SOB_ID
      ORDER BY CAJ.JOURNAL_ID, CAJ.CLOSING_GROUP, CAJ.ACCOUNT_CODE
      ;

  END SELECT_AUTO_JOURNAL;

-- 결산자동분개 삽입
  PROCEDURE INSERT_AUTO_JOURNAL
            ( P_JOURNAL_ID         OUT FI_CLOSING_AUTO_JOURNAL.JOURNAL_ID%TYPE
            , P_SOB_ID             IN FI_CLOSING_AUTO_JOURNAL.SOB_ID%TYPE
            , P_ORG_ID             IN FI_CLOSING_AUTO_JOURNAL.ORG_ID%TYPE
            , P_CLOSING_GROUP      IN FI_CLOSING_AUTO_JOURNAL.CLOSING_GROUP%TYPE
            , P_ACCOUNT_CONTROL_ID IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE       IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_CODE%TYPE
            , P_ACCOUNT_DR_CR      IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_DR_CR%TYPE
            , P_CONTRA_ACCOUNT_FLAG IN FI_CLOSING_AUTO_JOURNAL.CONTRA_ACCOUNT_FLAG%TYPE
            , P_REMARK             IN FI_CLOSING_AUTO_JOURNAL.REMARK%TYPE
            , P_USER_ID            IN FI_CLOSING_AUTO_JOURNAL.CREATED_BY%TYPE
            )
  AS
    V_SYSDATE DATE := GET_LOCAL_DATE(P_SOB_ID);
    V_RECORD_COUNT    NUMBER := 0;

  BEGIN
    BEGIN
      SELECT COUNT(CAJ.ACCOUNT_CONTROL_ID) AS RECORD_COUNT
        INTO V_RECORD_COUNT
        FROM FI_CLOSING_AUTO_JOURNAL CAJ
      WHERE CAJ.CLOSING_GROUP           = P_CLOSING_GROUP
        AND CAJ.ACCOUNT_CONTROL_ID      = P_ACCOUNT_CONTROL_ID
        AND CAJ.ACCOUNT_DR_CR           = P_ACCOUNT_DR_CR
        AND CAJ.SOB_ID                  = P_SOB_ID
      ;
    EXCEPTION WHEN OTHERS THEN
      V_RECORD_COUNT := 0;
    END;
    IF V_RECORD_COUNT <> 0 THEN
      RAISE ERRNUMS.Exist_Data;
    END IF;

    SELECT FI_CLOSING_AUTO_JOURNAL_S1.NEXTVAL
      INTO P_JOURNAL_ID
      FROM DUAL;

    INSERT INTO FI_CLOSING_AUTO_JOURNAL
    ( JOURNAL_ID
    , SOB_ID 
    , ORG_ID 
    , CLOSING_GROUP 
    , ACCOUNT_CONTROL_ID 
    , ACCOUNT_CODE 
    , ACCOUNT_DR_CR
    , CONTRA_ACCOUNT_FLAG 
    , REMARK 
    , CREATION_DATE 
    , CREATED_BY 
    , LAST_UPDATE_DATE 
    , LAST_UPDATED_BY )
    VALUES
    ( P_JOURNAL_ID
    , P_SOB_ID
    , P_ORG_ID
    , P_CLOSING_GROUP
    , P_ACCOUNT_CONTROL_ID
    , P_ACCOUNT_CODE
    , P_ACCOUNT_DR_CR
    , NVL(P_CONTRA_ACCOUNT_FLAG, 'N')
    , P_REMARK
    , V_SYSDATE
    , P_USER_ID
    , V_SYSDATE
    , P_USER_ID );

  EXCEPTION
    WHEN ERRNUMS.Exist_Data THEN
      RAISE_APPLICATION_ERROR(ERRNUMS.Exist_Data_Code, ERRNUMS.Exist_Data_Desc);
  END INSERT_AUTO_JOURNAL;

-- 결산자동분개 수정.
  PROCEDURE UPDATE_AUTO_JOURNAL
            ( W_JOURNAL_ID         IN FI_CLOSING_AUTO_JOURNAL.JOURNAL_ID%TYPE
            , W_SOB_ID             IN FI_CLOSING_AUTO_JOURNAL.SOB_ID%TYPE
            , P_CLOSING_GROUP      IN FI_CLOSING_AUTO_JOURNAL.CLOSING_GROUP%TYPE
            , P_ACCOUNT_CONTROL_ID IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_CONTROL_ID%TYPE
            , P_ACCOUNT_CODE       IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_CODE%TYPE
            , P_ACCOUNT_DR_CR      IN FI_CLOSING_AUTO_JOURNAL.ACCOUNT_DR_CR%TYPE
            , P_CONTRA_ACCOUNT_FLAG IN FI_CLOSING_AUTO_JOURNAL.CONTRA_ACCOUNT_FLAG%TYPE
            , P_REMARK             IN FI_CLOSING_AUTO_JOURNAL.REMARK%TYPE
            , P_USER_ID            IN FI_CLOSING_AUTO_JOURNAL.CREATED_BY%TYPE
            )
  AS
     V_SYSDATE DATE := GET_LOCAL_DATE(W_SOB_ID);
  BEGIN

    UPDATE FI_CLOSING_AUTO_JOURNAL
      SET CLOSING_GROUP      = P_CLOSING_GROUP
        , ACCOUNT_CONTROL_ID = P_ACCOUNT_CONTROL_ID
        , ACCOUNT_CODE       = P_ACCOUNT_CODE
        , ACCOUNT_DR_CR      = P_ACCOUNT_DR_CR
        , CONTRA_ACCOUNT_FLAG = NVL(P_CONTRA_ACCOUNT_FLAG, 'N')
        , REMARK             = P_REMARK
        , LAST_UPDATE_DATE   = V_SYSDATE
        , LAST_UPDATED_BY    = P_USER_ID
    WHERE JOURNAL_ID         = W_JOURNAL_ID
    ;

  END UPDATE_AUTO_JOURNAL;

-- 결산자동분개 삭제.
  PROCEDURE DELETE_AUTO_JOURNAL
            ( W_JOURNAL_ID         IN FI_CLOSING_AUTO_JOURNAL.JOURNAL_ID%TYPE
            )
  AS
  BEGIN
    DELETE FROM FI_CLOSING_AUTO_JOURNAL CAJ
    WHERE CAJ.JOURNAL_ID          = W_JOURNAL_ID
    ;
  END DELETE_AUTO_JOURNAL;

END FI_CLOSING_AUTO_JOURNAL_G;
/
